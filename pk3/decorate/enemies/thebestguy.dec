actor WeeabooRevenant : Revenant replaces Revenant
{
    +MISSILEMORE
    +MISSILEEVENMORE
    -LONGMELEERANGE
    +DONTBLAST
    Species "Body"
    PainChance "kubiuti", 255
    PainChance "MessiahDamage", 255
    PainChance "KubiutiUpper", 255
    PainChance "KharonSheathe", 255
    PainChance "SabbathDamage", 192
    PainChance "IronFistDamage", 255
    PainChance "KubiSpinAttack", 255
    PainChance "KubiLungeAttack", 255
    PainChance "KubiRangeAttack", 255
    PainChance "HaltHammerzeitImpact", 255
    DamageFactor "GuardMove", 0.0
    DamageFactor "EnemyInfightDamageRevenant", 0.0
    DamageFactor "ArchvileFire", 0.0
    DamageFactor "ExodusJump", 0.0
    SeeSound "revenant/see"
    PainSound "revenant/pain"
    ActiveSound "revenant/idle"
    DeathSound "revenant/death"
    States
    {
    See:
        SKEL AABBCCDDEEFF 2 A_Chase
        Loop
    See2:
        SKEL AABBCCDDEEFF 2 A_Wander
        goto See
    Melee:
    Missile:
        TNT1 A 0 A_FaceTarget
        SKEL K 25 A_PlaySound("revenant/gunready")
    Missile2:
        SKEL K 5 A_FaceTarget
        TNT1 A 0 A_FaceTarget
        SKEL J 7 BRIGHT A_CustomMissile("RevenantTracer",52,-12,0,0)//A_SkelMissile
        SKEL J 8
        SKEL K 5 A_FaceTarget
        TNT1 A 0 A_FaceTarget
        SKEL J 7 BRIGHT A_CustomMissile("RevenantTracer",52,12,0,0)
        SKEL J 8
        SKEL J 7 BRIGHT A_CustomMissile("RevenantTracer",52,-12,0,0)//A_SkelMissile
        SKEL J 8
        SKEL K 5 A_FaceTarget
        TNT1 A 0 A_FaceTarget
        SKEL J 7 BRIGHT A_CustomMissile("RevenantTracer",52,12,0,0)//A_SkelMissile
        SKEL J 8
        Goto See2

    Pain.KubiutiUpper:
        TNT1 A 0 A_Pain
        TNT1 A 0 A_GiveInventory("EnemyLaunchedResistance2",1)
        TNT1 A 0 A_GiveInventory("EnemyHasBeenLaunched",1)
    EnemyLaunch:
        TNT1 A 0 A_ChangeVelocity(-4,0,10,3)
        TNT1 A 0 A_SetInvulnerable
        SKEL MNO 1
    EnemyHang:      
        TNT1 A 0 A_UnSetInvulnerable
        //TNT1 A 0 A_ChangeVelocity(0,0,0,3)
        TNT1 A 0 A_LowGravity
    EnemyHang2:
        TNT1 A 0 A_GiveInventory("LaunchTimerCounter",1)
        TNT1 A 0 A_JumpIfInventory("LaunchTimerCounter",0,"ForceDrop")
        SKEL O 2 A_CheckFloor("EnemyDrop")
        loop
    ForceDrop:
        TNT1 A 0 A_GiveInventory("CannotLaunchAgain",1)
    EnemyDrop:
        TNT1 A 0 A_Gravity
        TNT1 A 0 A_TakeInventory("LaunchTimerCounter",0x7FFFFFFF)
        TNT1 A 0 A_TakeInventory("LaunchDamageCounter",10)
        TNT1 A 0 A_TakeInventory("EnemyLaunchedResistance2",1)
        TNT1 A 0 A_TakeInventory("EnemyHasBeenLaunched",1)
        SKEL ONM 1
        Goto See
    LaunchedPainKubiuti:
        TNT1 A 0 A_PlaySoundEx("sword/hit","soundslot5",0,0)
        TNT1 A 0 A_GiveToTarget("MidCombat",75)
    LaunchedPain:
        TNT1 A 0 A_GiveToTarget("HyperComboCounter",2)
        SKEL N 1 ThrustThingZ(0,8,0,0)
        TNT1 A 0 A_GiveInventory("LaunchDamageCounter",1)
        TNT1 A 0 A_JumpIfInventory("LaunchDamageCounter",10,"ForceDrop")
        SKEL N 3 A_Pain
        Goto EnemyHang2

    Pain.KubiLungeAttack:
    Pain.KubiSpinAttack:
        TNT1 A 0 A_JumpIfInventory("EnemyHasBeenLaunched",1,"LaunchedPainKubiuti")
        TNT1 A 0 A_PlaySoundEx("sword/hit","soundslot5",0,0)
        TNT1 A 0 A_GiveToTarget("HyperComboCounter",1)
        TNT1 A 0 A_GiveToTarget("MidCombat",75)
        goto Super::Pain
    Pain.KubiRangeAttack:
        TNT1 A 0 A_JumpIfInventory("EnemyHasBeenLaunched",1,"LaunchedPainKubiuti")
        TNT1 A 0 A_PlaySoundEx("sword/hit","soundslot5",0,0)
        TNT1 A 0 A_GiveToTarget("MidCombat",75)
        goto Super::Pain
    Pain.FrosthammerCharged:
    Pain.FrosthammerExplode:
    Pain.Frosthammer:
        TNT1 AAAAAAAAAA 0 A_SpawnItemEx("IceFog2",random(-1,1),random(-1,1),random(16,46),random(-500,500)/100.00,random(-500,500)/100.00,random(-200,500)/100.00,random(-180,180),161)
        TNT1 A 0 ACS_ExecuteAlways(267,0,WEEB_DEC_SLOWDOWN,0,0)
    Pain.SabbathDamage:
    Pain.KharonSheathe:
        TNT1 A 0 A_JumpIfInventory("EnemyHasBeenLaunched",1,"LaunchedPain")
        SKEL L 6 //A_Recoil(2)
        SKEL L 6 A_Pain
        Goto See
    Pain:
        TNT1 A 0 A_JumpIfInventory("EnemyHasBeenLaunched",1,"LaunchedPain")
        SKEL L 5
        SKEL L 5 A_Pain
        Goto See
    Death.KubiRangeAttack:
    Death.KubiLungeAttack:
    Death.KubiSpinAttack:
    Death.Knife:
        TNT1 A 0 A_PlaySoundEx("sword/hit","soundslot5",0,0)
        TNT1 A 0 A_GiveToTarget("HyperComboCounter",2)
        //TNT1 A 0 A_GiveToTarget("SuperMeterCounter",2)
        TNT1 A 0 A_GiveToTarget("MidCombat",75)
        goto death.kubiuti+5

    Death.HaltHammerzeitImpact:
    Death.HaltHammerzeit:
    Death.IronFistExplosion:
    Death.AcaciaDamage:
    Death.TestamentDamage:
    Death.SabbathDamage:
    Death.ExodusDamage:
        TNT1 A 0 A_JumpIfInTargetInventory("IAmASkilledPersonWhoWantsOnlyMySwordToGiveSouls",1,"Death")
        TNT1 AAAA 0 A_CustomMissile("SoulAmmo",random(24,42),random(-10,10),random(-180,180),0,random(-32,90))
    Death:
        TNT1 A 0 A_JumpIfInTargetInventory("IAmASkilledPersonWhoWantsOnlyMySwordToGiveSouls",1, 5)//A_GiveToTarget("SuperMeterCounter",4)
        TNT1 A 0 A_Gravity
        TNT1 A 0 A_TakeInventory("EnemyLaunchedResistance2",1)
        TNT1 A 0 A_TakeInventory("EnemyHasBeenLaunched",1)
        goto Super::Death

    Death.Bloody:
        SKEL RRR 2 A_ThrowGrenade ("BloodShower2",-2,5+random(0,7),5+random(0,7))
        SKEL S 0 A_Scream
        SKEL SS 2 A_ThrowGrenade ("BloodShower2",-2,5+random(0,7),5+random(0,7))
        SKEL T 0 A_NoBlocking
        SKEL TT 2 A_ThrowGrenade ("BloodShower2",-2,5+random(0,7),5+random(0,7))
        SKEL UUVVWWXX 2 A_ThrowGrenade ("BloodShower2",-2,5+random(0,7),5+random(0,7))
        SKEL YYYYY 2 A_SpawnDebris("BloodShower2",0)
        SKEL Y -1
        Stop
    Death.Kubiuti:
    Death.KubiutiUpper:
        TNT1 A 0 A_AlertMonsters
        TNT1 AAAA 0 A_CustomMissile("SoulAmmo",random(24,42),random(-10,10),random(-180,180),0,random(-32,90))//A_GiveToTarget("SuperMeterCounter",4)
        TNT1 A 0 A_Gravity
        TNT1 A 0 A_TakeInventory("EnemyLaunchedResistance2",1)
        TNT1 A 0 A_TakeInventory("EnemyHasBeenLaunched",1)
        TNT1 A 0 A_PlaySound("enemy/bisected")
        TNT1 A 0 A_Fall
        TNT1 AAAAA 0 A_CustomMissile("EnemySplashGus1",random(32,48),0,random(0,360),2,random(0,360))
        TNT1 A 0 A_Jump(128,"kubiuti2","death.Bloody")
        TNT1 A 0 A_SpawnItem("SKEXKubiuted",0,40,0)
        XDR1 EEEEEEEEEE 2 A_ThrowGrenade ("BloodSplashGus1",-2,5+random(0,7),40)
        TNT1 A 0 A_NoBlocking
        XDR1 FFFGGG 2 A_ThrowGrenade ("BloodSplashGus1",-2,5+random(0,7),40)
        XDR1 HHHII 2 A_SpawnDebris("BloodShower2",0)
        XDR1 I -1
        Stop
    Kubiuti2:
        TNT1 A 0 A_SpawnItem("SKEXKubiuted",0,40,0)
        TNT1 A 0 A_Recoil(-1)
        TNT1 A 0 A_Wander
        TNT1 A 0 A_PlaySoundEx("blood/spew","soundslot5",0,0)
        XDR1 ABC 3 A_ThrowGrenade ("BloodShower2",-2,5+random(0,7),5+random(0,7))
        TNT1 A 0 A_Recoil(-1)
        TNT1 A 0 A_Wander
        TNT1 A 0 A_PlaySoundEx("blood/spew","soundslot5",0,0)
        XDR1 DCB 3 A_ThrowGrenade ("BloodShower2",-2,5+random(0,7),5+random(0,7))
        TNT1 A 0 A_Jump(32,1)
        goto kubiuti2+1
        TNT1 A 0 A_PlaySoundEx("blood/spewstop","soundslot5",0,0)
        XDR1 EFGH 4 A_ThrowGrenade ("BloodShower2",-2,5+random(0,7),5+random(0,7))
        XDR1 I -1
        Stop

    Death.IronFistDamage:
        TNT1 AAAA 0 A_CustomMissile("SoulAmmo",random(24,42),random(-10,10),random(-180,180),0,random(-32,90))//A_GiveToTarget("SuperMeterCounter",3)
        TNT1 A 0
        TNT1 A 0 A_Gravity
        TNT1 A 0 A_TakeInventory("EnemyLaunchedResistance2",1)
        TNT1 A 0 A_TakeInventory("EnemyHasBeenLaunched",1)
        TNT1 AAAAAAAAAA 0 A_CustomMissile("BloodShower2",40,0,random(0,360),2,random(0,360))
        TNT1 AAAAA 0 A_CustomMissile("EnemySplashGus1",random(4,32),0,random(0,360),2,random(0,360))
        TNT1 AAAAA 0 A_CustomMissile("BloodShower5",random(0,32),0,random(0,360),2,random(0,360))
        SKEL L 2
        TNT1 A 0 A_XScream
        TNT1 AAAAAAAAAAAA 0 A_CustomMissile("BloodShower2",random(16,48),0,180+random(-45,45),2,random(0,75))
        TNT1 AAAAAA 0 A_CustomMissile("EnemySplashGus1",random(0,32),0,random(0,360),2,random(0,360))
        TNT1 AAAAAAAAAAAA 0 A_SpawnItemEx("TinyBone",random(-5,5),random(-5,5),random(12,40),random(-400,400)/100.00,random(-400,400)/100.00,random(300,800)/100.00,random(-180,180),160)
        TNT1 AAAAAAAAAAAA 0 A_SpawnItemEx("TinyBouncyBone",random(-5,5),random(-5,5),random(12,40),random(-400,400)/100.00,random(-400,400)/100.00,random(300,800)/100.00,random(-180,180),160)
        TNT1 AA 0 A_SpawnItemEx("Gore_Gib4",random(-10,10),random(-10,10),random(12,40),random(-700,700)/100.00,random(-700,700)/100.00,random(600,1400)/100.00,random(-180,180),160,64)
        TNT1 AA 0 A_SpawnItemEx("Gore_Gib6",random(-10,10),random(-10,10),random(12,40),random(-700,700)/100.00,random(-700,700)/100.00,random(600,1400)/100.00,random(-180,180),160,64)
        TNT1 AA 0 A_SpawnItemEx("Gore_Gib7",random(-10,10),random(-10,10),random(12,40),random(-700,700)/100.00,random(-700,700)/100.00,random(600,1400)/100.00,random(-180,180),160,64)
        TNT1 AA 0 A_SpawnItemEx("Gore_Gib8",random(-10,10),random(-10,10),random(12,40),random(-700,700)/100.00,random(-700,700)/100.00,random(600,1400)/100.00,random(-180,180),160,64)
        TNT1 AAAAA 0 A_CustomMissile("BloodShower5",random(32,48),0,random(0,360),2,random(0,360))
        TNT1 AAAAA 0 A_CustomMissile("BloodShower2",40,0,random(0,360),2,random(0,360))
        XDR2 AA 1 A_SpawnDebris("BloodShower2",1)
        TNT1 A 0 A_NoBlocking
        XDR2 BBBCCCDDDEEEFFF 1 A_SpawnDebris("BloodShower2",1)
        XDR2 F -1 A_BossDeath
        stop
    Death.UltimaBeam:
        TNT1 A 0 A_GiveToTarget("MidCombat",75)
        TNT1 A 0 A_Scream
        SKEL LLLLLLLL 2 A_FadeOut(0.1)
        TNT1 A 0 A_NoBlocking
        NONE A 1
        stop
    Death.FrosthammerCharged:
    Death.FrosthammerExplode:
    Death.Frosthammer:
        SKEL L 1
        TNT1 A 0 A_NoBlocking
        TNT1 A 0 A_PlaySound("misc/freeze")
        TNT1 A 0 A_PlaySoundEx("frost/freeze","soundslot6")
        TNT1 A 0 A_SpawnItemEx("CoolguyFrozen",0,0,0,0,0,0,0,32)
        NONE A 5
        Stop
    }
}

actor CoolguyFrozen : FrozenDummy
{
    Radius 20
    States
    {
    Spawn:
        SKEL L 2100
        SKEL L 1 A_Jump(128, "Death")
        Loop
    }
}

actor RevenantTracer2 : RevenantTracer replaces RevenantTracer
{
    Speed 14
    +THRUGHOST
    States
    {
    Spawn:
        FATB A 1 bright A_SeekerMissile(2,4)//A_Tracer
        FATB B 1 bright
        TNT1 A 0 A_GiveInventory("Counter",1)
        TNT1 A 0 A_JumpIfInventory("Counter",35,"Spawn2")
        loop
    Spawn2:
        FATB AB 1 bright
        loop
    Death:
        TNT1 A 0
        TNT1 A 0 A_SpawnItemEx("EnemyRevenantInfight",0,0,0,0,0,0,0,32)
        goto Super::Death
    }
}

actor SKEXKubiuted// : POSXKubiuted
{
    -SOLID
    +CLIENTSIDEONLY
    Radius 8
    Height 8
    Mass 500
    States
    {
    Spawn:
        TNT1 A 0
        TNT1 A 0 ThrustThing (random(0,255),random(1,4))
        TNT1 A 0 ThrustThingZ(0,random(15,40),0,0)
        XDF1 EEEEFFFFG 1 A_SpawnItem("BloodShower3",0)
        TNT1 A 0 A_CheckFloor("Spawned")
        goto Spawn+11
    Spawned:
        TNT1 A 0 A_PlaySound("S_Blodd")
        XDF1 H 1    
        XDF1 H -1
        stop
    }
}