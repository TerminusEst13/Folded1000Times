ACTOR Priest replaces Arachnotron
{
    Health 500
    Speed 12
    Radius 24
    Height 56
    Gravity 0.75
    Mass 600
    PainChance 192
    PainChance "kubiuti", 255
    PainChance "KubiutiUpper", 255
    PainChance "KharonSheathe", 255
    PainChance "IronFistDamage", 255
    PainChance "KubiSpinAttack", 255
    PainChance "KubiLungeAttack", 255
    PainChance "KubiRangeAttack", 255
    PainChance "HaltHammerzeitImpact", 255
    DamageFactor "GuardMove", 0.0
    DamageFactor "EnemyInfightDamageArachnotron", 0.0
    DamageFactor "ArchvileFire", 0.0
    DamageFactor "ExodusJump", 0.0
    Species "Body"
    Monster
    +FLOORCLIP
    +MISSILEMORE
    +MISSILEEVENMORE
    +BOSSDEATH
    +DONTBLAST
    SeeSound "priest/see"
    PainSound "priest/pain"
    DeathSound "priest/death"
    ActiveSound "priest/idle"
    Obituary "%o was desecrated by a Priest."
    States
    {
    Spawn:
        PRST A 10 A_Look
        Loop
    See:
        TNT1 A 0 A_ChangeFlag("PAINLESS",0)
        PRST AA 2 A_Chase
        PRST BB 2 A_FastChase
        PRST CC 2 A_Chase
        PRST DD 2 A_FastChase
        Loop
    Missile:
        PRST F 10 A_FaceTarget
        TNT1 A 0 A_Jump(32,"Missile3")
        TNT1 A 0 A_Jump(64,"Missile2")
        PRST F 4 A_FaceTarget
        TNT1 A 0 A_PlaySound("priest/shot")
        PRST G 4 BRIGHT A_CustomMissile("PriestFire",32,0,0,0,0)
        PRST ABCD 2 A_Wander
        TNT1 A 0 A_Jump(128,"See")
        TNT1 A 0 A_CPosRefire
        goto Missile
    Missile2:
        TNT1 A 0 A_Jump(256,"StrafeLeft","StrafeRight")
    StrafeLeft:
        TNT1 A 0 ThrustThingZ(0,random(20,30),0,1)
        TNT1 A 0 ThrustThing(angle*256/360+192,random(16,24),0,0)
        goto Missile2B
    StrafeRight:
        TNT1 A 0 ThrustThingZ(0,random(20,30),0,1)
        TNT1 A 0 ThrustThing(angle*256/360+64,random(16,24),0,0)
    Missile2B:
        PRST F 4 A_FaceTarget
        TNT1 A 0 A_PlaySound("priest/shot")
        PRST G 4 BRIGHT A_CustomMissile("PriestFire",32,0,0,0,0)
        PRST F 4 A_FaceTarget
        TNT1 A 0 A_PlaySound("priest/shot")
        PRST G 4 BRIGHT A_CustomMissile("PriestFire",32,0,0,0,0)
        PRST F 4 A_FaceTarget
        TNT1 A 0 A_PlaySound("priest/shot")
        PRST G 4 BRIGHT A_CustomMissile("PriestFire",32,0,0,0,0)
        goto See
    Missile3:
        TNT1 A 0 ThrustThingZ(0,random(50,70),0,1)
        TNT1 A 0 A_Recoil(random(4,8))
        TNT1 A 0 A_ChangeFlag("PAINLESS",1)
    Missile3B:
        PRST F 4 A_FaceTarget
        TNT1 A 0 A_PlaySound("priest/shot")
        PRST G 4 BRIGHT A_CustomMissile("PriestFire",32,0,0,0,0)
        TNT1 A 0 A_CheckFloor("See")
        goto Missile3B
    Pain2:
        TNT1 A 0
        TNT1 A 0 ThrustThingZ(0,random(15,30),0,1)
        TNT1 A 0 A_Recoil(random(4,8))
        PRST E 2 A_Pain
        Goto See
    Pain.Kubiuti:
        TNT1 A 0 A_JumpIfInventory("EnemyHasBeenLaunched",1,"LaunchedPainKubiuti")
    Pain:
        TNT1 A 0 A_JumpIfInventory("EnemyHasBeenLaunched",1,"LaunchedPain")
        PRST E 3 A_Stop
        PRST E 3 A_Pain
        Goto See
    Pain.FrosthammerCharged:
    Pain.FrosthammerExplode:
    Pain.Frosthammer:
        TNT1 AAAAAAAAAA 0 A_SpawnItemEx("IceFog2",random(-1,1),random(-1,1),random(16,46),random(-500,500)/100.00,random(-500,500)/100.00,random(-200,500)/100.00,random(-180,180),161)
        TNT1 A 0 ACS_ExecuteAlways(267,0,WEEB_DEC_SLOWDOWN,0,0)
    Pain.SabbathDamage:
    Pain.KharonSheathe:
        TNT1 A 0 A_JumpIfInventory("EnemyHasBeenLaunched",1,"LaunchedPain")
        PRST E 5 //A_Recoil(2)
        PRST E 5 A_Pain
        Goto See

    Pain.KubiutiUpper:
        TNT1 A 0 A_Pain
        TNT1 A 0 A_GiveInventory("EnemyLaunchedResistance2",1)
        TNT1 A 0 A_GiveInventory("EnemyHasBeenLaunched",1)
    EnemyLaunch:
        TNT1 A 0 A_ChangeVelocity(0,0,12,3)
        TNT1 A 0 A_SetInvulnerable
        PRST IJK 1
    EnemyHang:      
        TNT1 A 0 A_UnSetInvulnerable
        //TNT1 A 0 A_ChangeVelocity(0,0,0,3)
        //TNT1 A 0 A_LowGravity
    EnemyHang2:
        TNT1 A 0 A_GiveInventory("LaunchTimerCounter",1)
        TNT1 A 0 A_JumpIfInventory("LaunchTimerCounter",0,"ForceDrop")
        PRST K 2 A_CheckFloor("EnemyDrop")
        loop
    ForceDrop:
        TNT1 A 0 A_GiveInventory("CannotLaunchAgain",1)
    EnemyDrop:
        TNT1 A 0 A_Gravity
        TNT1 A 0 A_TakeInventory("LaunchTimerCounter",0x7FFFFFFF)
        TNT1 A 0 A_TakeInventory("LaunchDamageCounter",10)
        TNT1 A 0 A_TakeInventory("EnemyLaunchedResistance2",1)
        TNT1 A 0 A_TakeInventory("EnemyHasBeenLaunched",1)
        PRST KJI 1
        Goto See
    LaunchedPainKubiuti:
        TNT1 A 0 A_PlaySoundEx("steelbeast/hwandohit","soundslot6",0,0)
        TNT1 A 0 A_PlaySoundEx("sword/hit","soundslot5",0,0)
        TNT1 A 0 A_GiveToTarget("MidCombat",75)
    LaunchedPain:
        TNT1 A 0 A_GiveToTarget("HyperComboCounter",2)
        PRST J 1 ThrustThingZ(0,10,0,0)
        TNT1 A 0 A_GiveInventory("LaunchDamageCounter",1)
        TNT1 A 0 A_JumpIfInventory("LaunchDamageCounter",10,"ForceDrop")
        PRST J 3 A_Pain
        Goto EnemyHang2

    Pain.KubiLungeAttack:
    Pain.KubiSpinAttack:
        TNT1 A 0 A_JumpIfInventory("EnemyHasBeenLaunched",1,"LaunchedPainKubiuti")
        TNT1 A 0 A_PlaySoundEx("sword/hit","soundslot5",0,0)
        TNT1 A 0 A_GiveToTarget("HyperComboCounter",1)
        TNT1 A 0 A_GiveToTarget("MidCombat",75)
        goto Pain2
    Pain.KubiRangeAttack:
        TNT1 A 0 A_JumpIfInventory("EnemyHasBeenLaunched",1,"LaunchedPainKubiuti")
        TNT1 A 0 A_PlaySoundEx("sword/hit","soundslot5",0,0)
        TNT1 A 0 A_GiveToTarget("MidCombat",75)
        goto Pain
    Death.KubiLungeAttack:
    Death.KubiSpinAttack:
    Death.KubiRangeAttack:
    Death.Knife:
        TNT1 A 0 A_PlaySoundEx("sword/hit","soundslot5",0,0)
        TNT1 A 0 A_GiveToTarget("HyperComboCounter",2)
        TNT1 A 0 A_GiveToTarget("MidCombat",75)
        TNT1 A 0 A_PlaySound("enemy/bisected")
        goto Death

    Death.Kubiuti:
    Death.KubiutiUpper:
        TNT1 A 0 A_AlertMonsters
        TNT1 A 0 A_PlaySound("enemy/bisected")
        TNT1 AAAA 0 A_CustomMissile("SoulAmmo",random(24,42),random(-10,10),random(-180,180),0,random(-32,90))//A_GiveToTarget("SuperMeterCounter",3)
        goto Death
    Death.HaltHammerzeitImpact:
    Death.HaltHammerzeit:
    Death.IronFistExplosion:
    Death.AcaciaDamage:
    Death.TestamentDamage:
    Death.SabbathDamage:
    Death.ExodusDamage:
        TNT1 A 0 A_JumpIfInTargetInventory("IAmASkilledPersonWhoWantsOnlyMySwordToGiveSouls",1,"Death")
        TNT1 AAAA 0 A_CustomMissile("SoulAmmo",random(24,42),random(-10,10),random(-180,180),0,random(-32,90))
    Death:
        TNT1 A 0
        TNT1 A 0 A_Gravity
        TNT1 A 0 A_TakeInventory("EnemyLaunchedResistance2",1)
        TNT1 A 0 A_TakeInventory("EnemyHasBeenLaunched",1)
        TNT1 A 0 A_NoBlocking
        PRST H 2 A_Scream
        TNT1 AAAAAAAAAAAA 0 A_CustomMissile("BloodShower2",random(16,48),0,180+random(-45,45),2,random(0,75))
        TNT1 AAAAA 0 A_CustomMissile("EnemySplashGus1",random(4,32),0,random(0,360),2,random(0,360))
        PRST I 3 A_CustomMissile("BloodShower2",random(16,48),0,180+random(-45,45),2,random(0,75))
        PRST J 3 A_CustomMissile("BloodShower2",random(16,48),0,180+random(-45,45),2,random(0,75))
        PRST KLMN 4 A_CustomMissile("BloodShower2",random(16,48),0,180+random(-45,45),2,random(0,75))
        PRST O -1 A_BossDeath
        stop
    Death.IronFistDamage:
        TNT1 AAAA 0 A_CustomMissile("SoulAmmo",random(24,42),random(-10,10),random(-180,180),0,random(-32,90))//A_GiveToTarget("SuperMeterCounter",3)
        TNT1 A 0
        TNT1 A 0 A_Gravity
        TNT1 A 0 A_TakeInventory("EnemyLaunchedResistance2",1)
        TNT1 A 0 A_TakeInventory("EnemyHasBeenLaunched",1)
        TNT1 AAAAAAAAAA 0 A_CustomMissile("BloodShower2",40,0,random(0,360),2,random(0,360))
        TNT1 AAAAA 0 A_CustomMissile("EnemySplashGus1",random(4,32),0,random(0,360),2,random(0,360))
        TNT1 AAAAA 0 A_CustomMissile("BloodShower5",random(0,32),0,random(0,360),2,random(0,360))
        PRST P 2
        TNT1 A 0 A_XScream
        TNT1 AAAAAAAAAAAA 0 A_CustomMissile("BloodShower2",random(16,48),0,180+random(-45,45),2,random(0,75))
        TNT1 AAAAAA 0 A_CustomMissile("EnemySplashGus1",random(0,32),0,random(0,360),2,random(0,360))
        TNT1 AAAAA 0 A_SpawnItemEx("TinyBone",random(-5,5),random(-5,5),random(12,40),random(-400,400)/100.00,random(-400,400)/100.00,random(300,800)/100.00,random(-180,180),160)
        TNT1 AAAAA 0 A_SpawnItemEx("TinyBouncyBone",random(-5,5),random(-5,5),random(12,40),random(-400,400)/100.00,random(-400,400)/100.00,random(300,800)/100.00,random(-180,180),160)
        TNT1 A 0 A_SpawnItemEx("Gore_Gib0",random(-10,10),random(-10,10),random(12,40),random(-700,700)/100.00,random(-700,700)/100.00,random(600,1400)/100.00,random(-180,180),160,64)
        TNT1 A 0 A_SpawnItemEx("Gore_Gib1",random(-10,10),random(-10,10),random(12,40),random(-700,700)/100.00,random(-700,700)/100.00,random(600,1400)/100.00,random(-180,180),160,64)
        TNT1 A 0 A_SpawnItemEx("Gore_Gib2",random(-10,10),random(-10,10),random(12,40),random(-700,700)/100.00,random(-700,700)/100.00,random(600,1400)/100.00,random(-180,180),160,64)
        TNT1 A 0 A_SpawnItemEx("Gore_Gib3",random(-10,10),random(-10,10),random(12,40),random(-700,700)/100.00,random(-700,700)/100.00,random(600,1400)/100.00,random(-180,180),160,64)
        TNT1 A 0 A_SpawnItemEx("Gore_Gib4",random(-10,10),random(-10,10),random(12,40),random(-700,700)/100.00,random(-700,700)/100.00,random(600,1400)/100.00,random(-180,180),160,64)
        TNT1 A 0 A_SpawnItemEx("Gore_Gib5",random(-10,10),random(-10,10),random(12,40),random(-700,700)/100.00,random(-700,700)/100.00,random(600,1400)/100.00,random(-180,180),160,64)
        TNT1 A 0 A_SpawnItemEx("Gore_Gib6",random(-10,10),random(-10,10),random(12,40),random(-700,700)/100.00,random(-700,700)/100.00,random(600,1400)/100.00,random(-180,180),160,64)
        TNT1 A 0 A_SpawnItemEx("Gore_Gib7",random(-10,10),random(-10,10),random(12,40),random(-700,700)/100.00,random(-700,700)/100.00,random(600,1400)/100.00,random(-180,180),160,64)
        TNT1 A 0 A_SpawnItemEx("Gore_Gib8",random(-10,10),random(-10,10),random(12,40),random(-700,700)/100.00,random(-700,700)/100.00,random(600,1400)/100.00,random(-180,180),160,64)
        TNT1 AAAAA 0 A_CustomMissile("BloodShower5",random(32,48),0,random(0,360),2,random(0,360))
        TNT1 AAAAA 0 A_CustomMissile("BloodShower2",40,0,random(0,360),2,random(0,360))
        PRST QQ 1 A_SpawnDebris("BloodShower2",1)
        TNT1 A 0 A_NoBlocking
        PRST RRRSSSTTTUUUVVV 1 A_SpawnDebris("BloodShower2",1)
        PRST W -1 A_BossDeath
        stop
    Death.UltimaBeam:
        TNT1 A 0 A_GiveToTarget("MidCombat",75)
        TNT1 A 0 A_Scream
        PRST EEEEEEEE 2 A_FadeOut(0.1)
        TNT1 A 0 A_NoBlocking
        NONE A 1 A_BossDeath
        stop
    Death.FrosthammerCharged:
    Death.FrosthammerExplode:
    Death.Frosthammer:
        PRST E 1
        TNT1 A 0 A_NoBlocking
        TNT1 A 0 A_PlaySound("misc/freeze")
        TNT1 A 0 A_PlaySoundEx("frost/freeze","soundslot6")
        TNT1 A 0 A_SpawnItemEx("PriestguyFrozen",0,0,0,0,0,0,0,32)
        NONE A 5 A_BossDeath
        Stop
    }
}

actor PriestguyFrozen : FrozenDummy
{
    Radius 24
    Health 15
    States
    {
    Spawn:
        PRST Z 2100
        PRST Z 1 A_Jump(128, "Death")
        Loop
    }
}

ACTOR PriestFire
{
    Radius 13
    Height 8
    Speed 20
    Damage 5
    RenderStyle Add 
    Alpha 0.95
    Scale 0.9
    DeathSound "priest/shothit"
    DamageType "HeavyEnemyDamage"
    Projectile
    +RANDOMIZE
    +THRUGHOST
    States
    {
    Spawn:
        TNT1 AA 0 A_SpawnItemEx("PriestFireTrail",random(-50,50)/100.00,random(-50,50)/100.00,random(-50,50)/100.00,random(-150,150)/80.00,random(-150,150)/80.00,random(-150,150)/80.00,0,160)
        DMFX A 1 Bright A_SpawnItemEx("PriestFireTrail",0,0,0,0,0,0,0,160)
        TNT1 AA 0 A_SpawnItemEx("PriestFireTrail",random(-50,50)/100.00,random(-50,50)/100.00,random(-50,50)/100.00,random(-150,150)/80.00,random(-150,150)/80.00,random(-150,150)/80.00,0,160)
        DMFX B 1 Bright A_SpawnItemEx("PriestFireTrail",0,0,0,0,0,0,0,160)
        TNT1 AA 0 A_SpawnItemEx("PriestFireTrail",random(-50,50)/100.00,random(-50,50)/100.00,random(-50,50)/100.00,random(-150,150)/80.00,random(-150,150)/80.00,random(-150,150)/80.00,0,160)
        DMFX C 1 Bright A_SpawnItemEx("PriestFireTrail",0,0,0,0,0,0,0,160)
        Loop
    Death:
        TNT1 A 0
        TNT1 A 0 A_SpawnItemEx("EnemyArachnotronInfight",0,0,0,0,0,0,0,32)
        TNT1 A 0 A_Scream
        DMFX EFGH 2 Bright
        Stop
    }
}

actor PriestFireTrail
{
    +NOINTERACTION
    +CLIENTSIDEONLY
    +THRUACTORS
    +NOCLIP
    Alpha 0.75
    Scale 0.65
    RenderStyle Add
    States
    {
    Spawn:
        TNT1 A 0
        TNT1 A 0
        TNT1 A 0 ACS_ExecuteWithResult(275,WEEB_DEC_TOASTER)
        //TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(275,WEEB_DEC_TOASTER2) <= 0,"Toaster0")
        //TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(275,WEEB_DEC_TOASTER2) == 1,"Toaster1")
        //TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(275,WEEB_DEC_TOASTER2) >= 2,"Toaster2")
        TNT1 A 1
        Loop
    Toaster0:
        TNT1 A 2
        Goto Toaster0A
    Toaster0A:
        DMFX A 1 BRIGHT A_FadeOut(0.125)
        DMFX B 1 BRIGHT A_FadeOut(0.125)
        DMFX C 1 BRIGHT A_FadeOut(0.125)
        loop
    Toaster1:
        TNT1 A 2
        DMFX A 1 BRIGHT A_FadeOut(0.125)
        DMFX B 1 BRIGHT A_FadeOut(0.125)
        DMFX C 1 BRIGHT A_FadeOut(0.125)
        stop
    Toaster2:
        TNT1 A 0
        Stop
   }
}
