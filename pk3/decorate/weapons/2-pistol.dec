// ACACIA A-22
//   When Acacia Woodsmen Incorporated, a UK-based hunting gear manufacturer,
// announced their plans to branch out into weapon development, they were met
// with nothing but ridicule across the internet and the tabloids. Though they
// had plenty of success developing and marketing sporting equipment, designing
// hunting weapons was another thing entirely. Despite that, the company
// persevered and eventually brought out the Assault Pistol Model 22, which
// quickly took the hunting world by storm.
//   With its incredible accuracy, extremely moddable frame, use of massive
// .500 magnum rounds, and light weight, the A-22 in short order became the
// sportsman's weapon of choice. Many critics were quickly converted to the
// side of pistol hunting for large game, and it even enjoyed a temporary stint
// of infamy when it was picked up for a few years by PMCs as a powerful backup
// side-arm. Unfortunately, it wasn't too long before the biggest flaw of the
// weapon started becoming very irritating: Because of the sheer size of the
// rounds the magazine size had to be cut down in order to reduce overall
// weight, leaving a five-shot pistol that (while carving an incredibly
// persistent niche in the market) quickly fell out of favor of military
// applications.

//   Thankfully for you, the blessings of Heaven have rendered this flaw
// somewhat moot. Bullets just seem to automatically load themselves now,
// somehow.

actor "Kharon + Acacia A-22" : Kharon
{
    Weapon.AmmoUse2 1
    Weapon.AmmoType2 "WeebClip"
    Weapon.SlotNumber 2
    States
    {
    Ready:
        TNT1 A 0 A_JumpIfInventory("TimeToLaunchOctopus",1,"PrepareToHoldAttackNormal")
        TNT1 A 0 A_JumpIfInventory("WaitingTooLong",0,"IdleAnimation")
        TNT1 A 0 A_JumpIfInventory("HenshinActivated",1,"HenshinCheck")
        WEPA A 1 A_WeaponReady
        Loop
    Select:
        TNT1 A 0 A_JumpIfInventory("AlreadyInLevel",1,"Select3")
        TNT1 AAAAAAA 0 A_Raise
        TNT1 AA 1 A_Raise
        WAC1 C 0 A_PlayWeaponSound("sword/draw")
        TNT1 A 1 A_Raise
        KTUP ABCDE 1 A_Raise
        KTUP FG 1
        TNT1 A 12 A_GiveInventory("AlreadyInLevel",1)
        WEPA DCBA 1
        Goto Ready
    Select2:
        WEPA DCB 1 A_WeaponReady
        goto Ready
    Select3:
        TNT1 AAAAAAAAAAAAAAAAAAAA 0 A_Raise
        TNT1 A 1 A_Raise
        loop
    IdleAnimation:
        WEPA ABCD 2 A_WeaponReady
    IdleDecide:
        TNT1 A 0 A_Jump(16,"Idle0")
        TNT1 A 0 A_Jump(255,"Idle1","Idle2","Idle3","Idle4","Idle5")
        TNT1 A 1
        loop
    Idle0:
        TNT1 A 0
        TNT1 A 0 A_PlaySound("idle/raise")
        IDL0 ABCDEF 2 A_WeaponReady
        TNT1 A 0 A_PlaySound("haelin/idle0")
        IDL0 F 5 A_WeaponReady
        IDL0 F 10 A_WeaponReady
        IDL0 GHI 1 A_WeaponReady
        IDL0 J 10 A_WeaponReady
        IDL0 JKLMNOP 2 A_WeaponReady
        TNT1 A 5 A_WeaponReady
        goto IdleFinish
    Idle1:
        IDL1 ABC 2 A_WeaponReady
        IDL1 C 5 A_WeaponReady
        TNT1 A 0 A_PlaySound("haelin/idle1_1")
        IDL1 C 5 A_WeaponReady
        IDL1 DEFG 2 A_WeaponReady
        IDL1 G 30 A_WeaponReady
        TNT1 A 0 A_PlaySound("haelin/idle1_2")
        IDL1 HIJKLMNO 2 A_WeaponReady
        IDL1 O 12 A_WeaponReady
        TNT1 A 0 A_PlaySound("haelin/idle1_3")
        IDL1 PQRSTUV 2 A_WeaponReady
        IDL1 V 15 A_WeaponReady
        IDL1 WXY 2 A_WeaponReady
        TNT1 A 5 A_WeaponReady
        goto IdleFinish
    Idle2:
        IDL2 ABCCDDEEE 1 A_WeaponReady
        TNT1 A 0 A_PlaySound("idle/spray")
        IDL2 FGHH 2 A_WeaponReady
        TNT1 A 0 A_PlaySound("idle/spray")
        IDL2 FGHH 2 A_WeaponReady
        TNT1 A 0 A_PlaySound("idle/spray")
        IDL2 FGHH 2 A_WeaponReady
        IDL2 HHLLMNOPPQQ 1 A_WeaponReady
        TNT1 A 0 A_PlaySound("idle/rub")
        IDL2 TUVUT 2 A_WeaponReady
        TNT1 A 0 A_PlaySound("idle/rub")
        IDL2 TUVUT 2 A_WeaponReady
        TNT1 A 0 A_PlaySound("idle/rub")
        IDL2 TUVUT 2 A_WeaponReady
        TNT1 A 0 A_PlaySound("idle/rub")
        IDL2 TUVUTT 2 A_WeaponReady
        IDL2 WWWXXYYZ 1 A_WeaponReady
        TNT1 A 5 A_WeaponReady
        goto IdleFinish
    Idle3:
        IDL3 ABCDDEE 1 A_WeaponReady
        IDL3 F 10 A_WeaponReady
        TNT1 A 0 A_PlaySound("idle/shing")
        IDL3 FFGHIJK 1 A_WeaponReady
        IDL3 L 20 A_WeaponReady
        IDL3 MMNO 1 A_WeaponReady
        TNT1 A 5 A_WeaponReady
        goto IdleFinish
    Idle4:
        TNT1 A 2 A_WeaponReady
        IDL4 ABCDDEEFFGG 1 A_WeaponReady
        TNT1 A 0 A_PlaySound("haelin/idleknux")
        IDL4 H 7 A_WeaponReady
        IDL4 IIJJKKLMNOPQRRSSTT 1 A_WeaponReady
        TNT1 A 0 A_PlaySound("haelin/idleknux")
        IDL4 U 7 A_WeaponReady
        IDL4 VVWWXYZ 1 A_WeaponReady
        IDL4 [ 1 A_WeaponReady
        TNT1 A 2 A_WeaponReady
        goto IdleFinish
    Idle5:
        TNT1 A 2 A_WeaponReady
        IDL5 AABBCCD 1 A_WeaponReady
        TNT1 A 0 A_PlaySound("idle/swing")
        IDL5 EFFGGH 1 A_WeaponReady
        TNT1 A 0 A_PlaySound("idle/swing")
        IDL5 IJKLLMMN 1 A_WeaponReady
        TNT1 A 0 A_PlaySound("idle/swing2")
        IDL5 O 2 A_WeaponReady
        IDL5 PQ 3 A_WeaponReady
        IDL5 RS 2 A_WeaponReady
        TNT1 A 0 A_PlaySound("idle/pose")
        IDL5 T 15 A_WeaponReady
        IDL5 U 1 A_WeaponReady
        TNT1 A 0 A_PlaySound("idle/pose")
        IDL5 VWX 1 A_WeaponReady
        IDL5 Y 15 A_WeaponReady
        IDL5 Z 2 A_WeaponReady
        IDL5 [ 2 A_WeaponReady
        TNT1 A 2 A_WeaponReady
        goto IdleFinish
    IdleFinish:
        WEPA DCBA 2 A_WeaponReady
        TNT1 A 0 A_TakeInventory("WaitingTooLong",0x7FFFFFFF)
        goto Ready



    Fire:
        TNT1 A 0 //A_TakeInventory("PointsNoAttacks",1)
        TNT1 A 0 A_JumpIfInventory("ShootingLikeABaka",1,"FirePrepare")
        TNT1 A 0 A_JumpIfInventory("SlashingLikeAGaijin",1,"FireSequence1")
        WEPA BCD 1
        TNT1 A 0 A_GiveInventory("SlashingLikeAGaijin",1)
        goto FireSequence1
    FirePrepare:
        WEPA IH 1 A_TakeInventory("ShootingLikeABaka")
        TNT1 A 0 A_GiveInventory("SlashingLikeAGaijin",1)
        goto FireSequence1

    FireSequence1: // Right to left
        TNT1 A 0 A_PlaySoundEX("sword/swingmed","SoundSlot7")
        TNT1 A 0 A_JumpIfInventory("KharonSwung",1,"FireSequence2")
        TNT1 A 0 A_Jump(255,"FireSequence1A","FireSequence1B","FireSequence1C")
        TNT1 A 0
        TNT1 A 1
        goto FireSequence1+2
    FireSequence1A:
        KATN F 1
        KATN G 1
        TNT1 A 0 A_FireCustomMissile("KharonGuard",0,0) 
        TNT1 A 0 A_FireCustomMissile("KharonAttack3",0,0)
        KATN H 1
        TNT1 A 0 A_FireCustomMissile("KharonGuard",0,0) 
        TNT1 A 0 A_FireCustomMissile("KharonAttack3b",0,0)
        KATN I 1 A_GiveInventory("KharonSwung",1)
        KATN J 1
        TNT1 A 1 A_WeaponReady(WRF_NOSWITCH)
        goto Fired
    FireSequence1B:
        KATN K 1
        KATN L 1
        TNT1 A 0 A_FireCustomMissile("KharonGuard",0,0) 
        TNT1 A 0 A_FireCustomMissile("KharonAttack2",0,0)
        KATN M 1
        TNT1 A 0 A_FireCustomMissile("KharonGuard",0,0) 
        TNT1 A 0 A_FireCustomMissile("KharonAttack2b",0,0)
        KATN N 1 A_GiveInventory("KharonSwung",1)
        KATN O 1
        KATN P 1 A_WeaponReady(WRF_NOSWITCH)
        goto Fired
    FireSequence1C:
        KATN V 1
        KATN W 1
        TNT1 A 0 A_FireCustomMissile("KharonGuard",0,0) 
        TNT1 A 0 A_FireCustomMissile("KharonAttack1",0,0)
        KATN X 1
        TNT1 A 0 A_FireCustomMissile("KharonGuard",0,0) 
        TNT1 A 0 A_FireCustomMissile("KharonAttack1b",0,0)
        KATN Y 1 A_GiveInventory("KharonSwung",1)
        KATN Z 1
        TNT1 A 1 A_WeaponReady(WRF_NOSWITCH)
        goto Fired

    FireSequence2: // Left to right
        TNT1 A 0 A_Jump(255,"FireSequence2A","FireSequence2B","FireSequence2C")
        TNT1 A 0
        TNT1 A 1
        loop
    FireSequence2A:
        KATN A 1
        KATN B 1
        TNT1 A 0 A_FireCustomMissile("KharonGuard",0,0) 
        TNT1 A 0 A_FireCustomMissile("KharonAttack1",0,0)
        KATN C 1
        TNT1 A 0 A_FireCustomMissile("KharonGuard",0,0) 
        TNT1 A 0 A_FireCustomMissile("KharonAttack1b",0,0)
        KATN D 1 A_TakeInventory("KharonSwung",1)
        KATN E 1
        TNT1 A 1 A_WeaponReady(WRF_NOSWITCH)
        goto Fired
    FireSequence2B:
        KTUP A 1
        KTUP B 1
        TNT1 A 0 A_FireCustomMissile("KharonGuard",0,0) 
        TNT1 A 0 A_FireCustomMissile("KharonAttack2",0,0)
        KTUP C 1
        TNT1 A 0 A_FireCustomMissile("KharonGuard",0,0) 
        TNT1 A 0 A_FireCustomMissile("KharonAttack2b",0,0)
        KTUP D 1 A_TakeInventory("KharonSwung",1)
        KTUP E 1
        KTUP F 1 A_WeaponReady(WRF_NOSWITCH)
        goto Fired
    FireSequence2C:
        KATN Q 1
        KATN R 1
        TNT1 A 0 A_FireCustomMissile("KharonGuard",0,0) 
        TNT1 A 0 A_FireCustomMissile("KharonAttack3",0,0)
        KATN S 1
        TNT1 A 0 A_FireCustomMissile("KharonGuard",0,0) 
        TNT1 A 0 A_FireCustomMissile("KharonAttack3b",0,0)
        KATN T 1 A_TakeInventory("KharonSwung",1)
        KATN U 1
        TNT1 A 1 A_WeaponReady(WRF_NOSWITCH)
        goto Fired

    Hold:
        TNT1 A 0 //A_TakeInventory("PointsNoAttacks",1)
        TNT1 A 1 A_WeaponReady(WRF_NOSECONDARY | WRF_NOSWITCH)
        TNT1 A 0 A_JumpIfInventory("TimeToLaunchOctopus",1,"PrepareToHoldAttack")
        TNT1 A 0 A_JumpIfInventory("DoubleTapBack",1,"RepelWave")
        TNT1 A 0 A_JumpIfInventory("DoubleTapForward",1,"LungeAttack")
        TNT1 A 0 A_JumpIfInventory("DoubleTapLeft",1,"SpinAttackLeft")
        TNT1 A 0 A_JumpIfInventory("DoubleTapRight",1,"SpinAttackRight")
        TNT1 A 0 A_GiveInventory("SwordAttack",1)
        TNT1 A 0 A_Refire
        TNT1 A 0 A_ClearRefire
        TNT1 A 1 A_WeaponReady(WRF_NOSECONDARY | WRF_NOSWITCH)
        goto Fired2

    Fired:
        TNT1 A 10 A_WeaponReady(WRF_NOSWITCH)
        TNT1 A 0 A_ReFire
    Fired2:
        //TNT1 A 0 A_JumpIfInventory("SwordAttack",6,"HoldAttack")
        TNT1 A 0 A_TakeInventory("SwordAttack",0x7FFFFFFF)
        TNT1 A 5 A_WeaponReady(WRF_NOSWITCH)
        TNT1 A 0 A_TakeInventory("KharonSwung",1)
        TNT1 A 0 A_TakeInventory("SlashingLikeAGaijin",1)
        TNT1 A 0 A_TakeInventory("ShootingLikeABaka",1)
        TNT1 A 0 A_ClearRefire
        Goto Select2
    PrepareToHoldAttackGun:
        WEPA IH 1 A_WeaponReady(WRF_NOSWITCH | WRF_NOFIRE)
    PrepareToHoldAttack:
        TNT1 A 0 A_TakeInventory("SheatheSwung",1)
        TNT1 A 0 A_TakeInventory("KharonSwung",1)
        TNT1 A 0 A_TakeInventory("SlashingLikeAGaijin",1)
        TNT1 A 0 A_TakeInventory("ShootingLikeABaka",1)
        TNT1 A 0 A_TakeInventory("SwordAttack",0x7FFFFFFF)
        TNT1 A 0 A_ClearRefire
        TNT1 A 1
        goto HoldAttack
    PrepareToHoldAttackNormal:
        WEPA BCD 1 A_WeaponReady(WRF_NOSWITCH | WRF_NOFIRE)
    HoldAttack:
        TNT1 A 0 //A_TakeInventory("PointsNoAttacks",1)
        TNT1 A 0 A_TakeInventory("TimeToLaunchOctopus", 1)
        TNT1 A 1
        //TNT1 A 0 A_Recoil(-2)
        TNT1 A 0 A_SetPitch(pitch-0.5)
        TNT1 A 0 A_PlaySoundEx("sword/swingheavy","soundslot7",0,0)
        KATH A 1
        TNT1 A 0 A_SetPitch(pitch-1)
        TNT1 A 0 A_TakeInventory("SwordAttack",0x7FFFFFF)
        KATH B 1
        TNT1 A 0 A_FireCustomMissile("KharonGuardReflect",0,0)
        TNT1 A 0 A_FireCustomMissile("KharonAttackHeavy",0,0)
        KATH C 1
        TNT1 A 0 A_SetPitch(pitch-2)
        //TNT1 A 0 A_FireCustomMissile("KharonGuard",0,0)
        //TNT1 A 0 A_FireCustomMissile("KharonAttackHeavyB",0,0)
        KATH D 1 A_TakeInventory("KharonSwung",1)
        KATH E 1
        TNT1 A 2
        TNT1 A 0 A_SetPitch(pitch+2)
        TNT1 A 2
        TNT1 A 0 A_SetPitch(pitch+1)
        TNT1 A 1
        TNT1 A 0 A_SetPitch(pitch+0.5)
        //TNT1 A 5 A_WeaponReady(WRF_NOPRIMARY | WRF_NOSWITCH)
        goto Fired2



    SpinAttackLeft:
        TNT1 A 0 //A_TakeInventory("PointsNoSpecials",1)
        TNT1 A 1 A_SetAngle(angle+11.25)
        TNT1 A 1 A_SetAngle(angle+22.5)
        TNT1 A 0 A_TakeInventory("SuperMeterCounter",20)
        TNT1 A 0 A_PlayWeaponSound("sword/spinattack")
        TNT1 A 0 A_PlaySoundEx("haelin/attack","soundslot6",0,0)
        TNT1 A 0 A_PlaySoundEx("sword/swingheavy","soundslot7",0,0)
        TNT1 A 0 A_SpawnItemEx("KharonExplosion",0,0,0,0,0,0,0,48)
        TNT1 A 0 A_SpawnItemEx("ShockwaveSpawner",0,0,0,0,0,0,0,32)
        TNT1 A 0 A_CheckFloor("SpinAttackLeftContinue")
        TNT1 A 0 ThrustThingZ(0,24,0,0)
    SpinAttackLeftContinue:
        KSPC G 1 A_SetAngle(angle+45)
        KSPC H 1 A_SetAngle(angle+50)
        KSPC I 1 A_SetAngle(angle+55)
        KSPC J 1 A_SetAngle(angle+55)
        KSPC K 1 A_SetAngle(angle+50)
        KSPC L 1 A_SetAngle(angle+45)
        TNT1 A 1 A_SetAngle(angle+22.5)
        TNT1 A 1 A_SetAngle(angle+11.25)
        goto SpecialAttackFinish

    SpinAttackRight:
        TNT1 A 0 //A_TakeInventory("PointsNoSpecials",1)
        TNT1 A 1 A_SetAngle(angle-11.25)
        TNT1 A 1 A_SetAngle(angle-22.5)
        TNT1 A 0 A_TakeInventory("SuperMeterCounter",20)
        TNT1 A 0 A_PlayWeaponSound("sword/spinattack")
        TNT1 A 0 A_PlaySoundEx("haelin/attack","soundslot6",0,0)
        TNT1 A 0 A_PlaySoundEx("sword/swingheavy","soundslot7",0,0)
        TNT1 A 0 A_SpawnItemEx("KharonExplosion",0,0,0,0,0,0,0,48)
        TNT1 A 0 A_SpawnItemEx("ShockwaveSpawner",0,0,0,0,0,0,0,32)
        TNT1 A 0 A_CheckFloor("SpinAttackRightContinue")
        TNT1 A 0 ThrustThingZ(0,28,0,0)
    SpinAttackRightContinue:
        KSPC M 1 A_SetAngle(angle-45)
        KSPC N 1 A_SetAngle(angle-50)
        KSPC O 1 A_SetAngle(angle-55)
        KSPC P 1 A_SetAngle(angle-55)
        KSPC Q 1 A_SetAngle(angle-50)
        KSPC R 1 A_SetAngle(angle-45)
        TNT1 A 1 A_SetAngle(angle-22.5)
        TNT1 A 1 A_SetAngle(angle-11.25)
        goto SpecialAttackFinish

    LungeAttack:
        TNT1 A 0 A_CheckFloor("LungeAttackRise")
    LungeAttackPrepare:
        TNT1 A 0 //A_TakeInventory("PointsNoSpecials",1)
        TNT1 A 0 A_TakeInventory("SuperMeterCounter",20)
        TNT1 A 0 A_ChangeVelocity(0,0,0,CVF_REPLACE)
        TNT1 A 0 A_ChangeFlag("NOGRAVITY",1)
        TNT1 A 0 A_ChangeFlag("GHOST",1)
        TNT1 A 0 ACS_ExecuteAlways(WEEB_DECORATE,0,WEEB_DEC_FREEZE,0,0)
        TNT1 A 0 A_ChangeFlag("THRUSPECIES",1)
        TNT1 A 1
        TNT1 A 0 A_PlayWeaponSound("sword/beam3")
        TNT1 A 0 A_PlaySoundEx("haelin/attack","soundslot6",0,0)
        TNT1 AA 0 A_SpawnItemEx("RazorWindExplosion",0,0,0,0,0,0,0,48)
        TNT1 A 0 A_SpawnItemEx("RazorWind",0,0,32,125,0,0,0,48)
        TNT1 A 0 A_SpawnItemEx("RazorWind",0,0,32,124,0,0,1,48)
        TNT1 A 0 A_SpawnItemEx("RazorWind",0,0,32,124,0,0,-1,48)
        TNT1 A 0 A_SpawnItemEx("RazorWind",0,0,32,123,0,0,2,48)
        TNT1 A 0 A_SpawnItemEx("RazorWind",0,0,32,123,0,0,-2,48)
        TNT1 A 0 A_SpawnItemEx("RazorWind",0,0,32,122,0,0,3,48)
        TNT1 A 0 A_SpawnItemEx("RazorWind",0,0,32,122,0,0,-3,48)
        TNT1 A 0 A_SpawnItemEx("RazorWind",0,0,32,121,0,0,4,48)
        TNT1 A 0 A_SpawnItemEx("RazorWind",0,0,32,121,0,0,-4,48)
        TNT1 A 0 ThrustThing(angle*256/360,120,1,0)
    LungeAttackContinue:
        KSPC ABCDEF 1
        TNT1 A 1
        TNT1 A 0 A_ChangeVelocity(0,0,0,CVF_REPLACE)
        TNT1 A 0 A_Stop // You'd think this would be redundant.
        TNT1 A 0 ACS_ExecuteAlways(WEEB_DECORATE,0,WEEB_DEC_UNFREEZE,0,0)
        TNT1 A 5 A_SpawnItemEx("TelefragHack",0)
        TNT1 A 0 A_ChangeFlag("NOGRAVITY",0)
        TNT1 A 0 A_ChangeFlag("THRUSPECIES",0)
        TNT1 A 0 A_ChangeFlag("GHOST",0)
        goto SpecialAttackFinish
    LungeAttackRise:
        TNT1 A 1 ThrustThingZ(0,24,0,0)
        goto LungeAttackPrepare

    RepelWave:
        TNT1 A 0 //A_TakeInventory("PointsNoSpecials",1)
        KBLK ABCDEE 1
        KBLK F 15 A_PlayWeaponSound("sword/block")
        KSPC STU 1
        TNT1 A 5
        KSPC VW 1
        TNT1 A 0 A_TakeInventory("SuperMeterCounter",20)
        TNT1 A 0 A_PlayWeaponSound("sword/reflect")
        TNT1 A 0 A_PlaySoundEx("haelin/attack","soundslot6",0,0)
        TNT1 A 0 A_FireCustomMissile("RazorWind2",0,0,0,0,0,0)
        TNT1 A 0 A_FireCustomMissile("RazorWind2",-3,0,0,0,0,0)
        TNT1 A 0 A_FireCustomMissile("RazorWind2",3,0,0,0,0,0)
        TNT1 A 0 A_FireCustomMissile("RazorWind2",-6,0,0,0,0,0)
        TNT1 A 0 A_FireCustomMissile("RazorWind2",6,0,0,0,0,0)
        TNT1 A 0 A_FireCustomMissile("RazorWind2",-9,0,0,0,0,0)
        TNT1 A 0 A_FireCustomMissile("RazorWind2",9,0,0,0,0,0)
        TNT1 A 0 A_FireCustomMissile("RazorWind2",-12,0,0,0,0,0)
        TNT1 A 0 A_FireCustomMissile("RazorWind2",12,0,0,0,0,0)
        TNT1 A 0 A_FireCustomMissile("RazorWind2",-15,0,0,0,0,0)
        TNT1 A 0 A_FireCustomMissile("RazorWind2",15,0,0,0,0,0)
        KSPC XYZ 1
        TNT1 AA 1
        goto SpecialAttackFinish

    SpecialAttackFinish:
        TNT1 A 0 A_TakeInventory("SwordAttack",0x7FFFFFFF)
        TNT1 A 0 A_TakeInventory("KharonSwung",1)
        TNT1 A 0 A_TakeInventory("SlashingLikeAGaijin",1)
        TNT1 A 0 A_TakeInventory("ShootingLikeABaka",1)
        TNT1 A 0 A_TakeInventory("DoubleTapBack", 1)
        TNT1 A 0 A_TakeInventory("DoubleTapLeft", 1)
        TNT1 A 0 A_TakeInventory("DoubleTapRight", 1)
        TNT1 A 0 A_TakeInventory("DoubleTapForward", 1)
        TNT1 A 0 A_ClearRefire
        goto Fired2



    AltFire:
        TNT1 A 0 //A_TakeInventory("PointsNoAttacks",1)
        TNT1 A 0 A_JumpIfInventory("SlashingLikeAGaijin",1,"AltFirePrepare")
        TNT1 A 0 A_JumpIfInventory("ShootingLikeABaka",1,"AltFireGun")
        WEPA EFG 1
        TNT1 A 0 A_GiveInventory("ShootingLikeABaka",1)
    AltFireGun:
        HPIS A 1 A_WeaponReady(WRF_NOFIRE | WRF_NOSWITCH)
        TNT1 A 0 A_JumpIfNoAmmo("Nope")
        //TNT1 A 0 A_GunFlash
        TNT1 A 0 A_Light1
        TNT1 A 0 A_AlertMonsters
        TNT1 A 0 A_PlayWeaponSound("pistol/fire")
        TNT1 A 0 A_FireBullets(0,0,1,0,"BlankPuff", FBF_NORANDOM )
        TNT1 A 0 A_FireCustomMissile("AcaciaCasingSpawner",0,0,0,0,2)
        TNT1 A 0 A_FireCustomMissile("HuntingPistolBullet",0,1,0,1)
        TNT1 A 0 A_Recoil(0.2)
        TNT1 A 0 A_JumpIfInventory("IAmASillyPersonWhoDoesntLikeRecoil",1,"NoRecoil")
        TNT1 A 0 A_SetAngle(angle+(random(-400,400)/1000.000))
        TNT1 A 0 A_SetPitch(pitch-(random(200,700)/1000.000))
    NoRecoil:
        HPIS BC 1 Bright A_WeaponReady(WRF_NOFIRE | WRF_NOSWITCH)
        TNT1 A 0 A_Light0
        HPIS DEDDCCFF 1 A_WeaponReady(WRF_NOSWITCH)
        TNT1 A 0 A_JumpIfInventory("TimeToLaunchOctopus",1,"PrepareToHoldAttackGun")
        HPIS A 2 A_WeaponReady
        TNT1 A 0 A_JumpIfInventory("TimeToLaunchOctopus",1,"PrepareToHoldAttackGun")
        HPIS A 2 A_WeaponReady
        TNT1 A 0 A_JumpIfInventory("TimeToLaunchOctopus",1,"PrepareToHoldAttackGun")
        HPIS A 2 A_WeaponReady
        TNT1 A 0 A_JumpIfInventory("TimeToLaunchOctopus",1,"PrepareToHoldAttackGun")
        HPIS A 2 A_WeaponReady
        TNT1 A 0 A_JumpIfInventory("TimeToLaunchOctopus",1,"PrepareToHoldAttackGun")
        HPIS A 2 A_WeaponReady
        TNT1 A 0 A_JumpIfInventory("TimeToLaunchOctopus",1,"PrepareToHoldAttackGun")
        HPIS A 2 A_WeaponReady
        TNT1 A 0 A_JumpIfInventory("TimeToLaunchOctopus",1,"PrepareToHoldAttackGun")
        HPIS A 2 A_WeaponReady
        TNT1 A 0 A_JumpIfInventory("TimeToLaunchOctopus",1,"PrepareToHoldAttackGun")
        HPIS A 2 A_WeaponReady
        TNT1 A 0 A_JumpIfInventory("TimeToLaunchOctopus",1,"PrepareToHoldAttackGun")
        HPIS A 2 A_WeaponReady
        TNT1 A 0 A_JumpIfInventory("TimeToLaunchOctopus",1,"PrepareToHoldAttackGun")
        HPIS A 2 A_WeaponReady // 10
        TNT1 A 0 A_JumpIfInventory("TimeToLaunchOctopus",1,"PrepareToHoldAttackGun")
        HPIS A 2 A_WeaponReady
        TNT1 A 0 A_JumpIfInventory("TimeToLaunchOctopus",1,"PrepareToHoldAttackGun")
        HPIS A 2 A_WeaponReady
        TNT1 A 0 A_JumpIfInventory("TimeToLaunchOctopus",1,"PrepareToHoldAttackGun")
        HPIS A 2 A_WeaponReady
        TNT1 A 0 A_JumpIfInventory("TimeToLaunchOctopus",1,"PrepareToHoldAttackGun")
        HPIS A 2 A_WeaponReady
        TNT1 A 0 A_JumpIfInventory("TimeToLaunchOctopus",1,"PrepareToHoldAttackGun")
        HPIS A 2 A_WeaponReady
        TNT1 A 0 A_JumpIfInventory("TimeToLaunchOctopus",1,"PrepareToHoldAttackGun")
        HPIS A 2 A_WeaponReady
        TNT1 A 0 A_JumpIfInventory("TimeToLaunchOctopus",1,"PrepareToHoldAttackGun")
        HPIS A 2 A_WeaponReady
        TNT1 A 0 A_JumpIfInventory("TimeToLaunchOctopus",1,"PrepareToHoldAttackGun")
        HPIS A 2 A_WeaponReady
        TNT1 A 0 A_JumpIfInventory("TimeToLaunchOctopus",1,"PrepareToHoldAttackGun")
        HPIS A 2 A_WeaponReady
        TNT1 A 0 A_JumpIfInventory("TimeToLaunchOctopus",1,"PrepareToHoldAttackGun")
        HPIS A 2 A_WeaponReady // 20
        TNT1 A 0 A_JumpIfInventory("TimeToLaunchOctopus",1,"PrepareToHoldAttackGun")
        HPIS A 2 A_WeaponReady
        TNT1 A 0 A_JumpIfInventory("TimeToLaunchOctopus",1,"PrepareToHoldAttackGun")
        HPIS A 2 A_WeaponReady
        TNT1 A 0 A_JumpIfInventory("TimeToLaunchOctopus",1,"PrepareToHoldAttackGun")
        HPIS A 2 A_WeaponReady
        TNT1 A 0 A_JumpIfInventory("TimeToLaunchOctopus",1,"PrepareToHoldAttackGun")
        HPIS A 2 A_WeaponReady
        TNT1 A 0 A_JumpIfInventory("TimeToLaunchOctopus",1,"PrepareToHoldAttackGun")
        HPIS A 2 A_WeaponReady
        TNT1 A 0 A_JumpIfInventory("TimeToLaunchOctopus",1,"PrepareToHoldAttackGun")
        HPIS A 2 A_WeaponReady
        TNT1 A 0 A_JumpIfInventory("TimeToLaunchOctopus",1,"PrepareToHoldAttackGun")
        HPIS A 2 A_WeaponReady
        TNT1 A 0 A_JumpIfInventory("TimeToLaunchOctopus",1,"PrepareToHoldAttackGun")
        HPIS A 2 A_WeaponReady
        TNT1 A 0 A_JumpIfInventory("TimeToLaunchOctopus",1,"PrepareToHoldAttackGun")
        HPIS A 2 A_WeaponReady
        TNT1 A 0 A_JumpIfInventory("TimeToLaunchOctopus",1,"PrepareToHoldAttackGun")
        HPIS A 2 A_WeaponReady // 30
        TNT1 A 0 A_JumpIfInventory("TimeToLaunchOctopus",1,"PrepareToHoldAttackGun")
        HPIS A 2 A_WeaponReady
        TNT1 A 0 A_JumpIfInventory("TimeToLaunchOctopus",1,"PrepareToHoldAttackGun")
        HPIS A 2 A_WeaponReady
        TNT1 A 0 A_JumpIfInventory("TimeToLaunchOctopus",1,"PrepareToHoldAttackGun")
        HPIS A 2 A_WeaponReady
        TNT1 A 0 A_JumpIfInventory("TimeToLaunchOctopus",1,"PrepareToHoldAttackGun")
        HPIS A 2 A_WeaponReady
        TNT1 A 0 A_JumpIfInventory("TimeToLaunchOctopus",1,"PrepareToHoldAttackGun")
        HPIS A 2 A_WeaponReady // 35
        Goto AltFired
    Nope:
        TNT1 A 0 A_PlaySoundEx("pistol/dry","soundslot7",0,0)
        HPIS A 10 A_WeaponReady(WRF_NOSECONDARY | WRF_NOSWITCH)
        TNT1 A 0 A_JumpIfInventory("IAmAnOkayPersonWhoLikesToAutoSwitch",1,"DeselectShit")
        TNT1 A 0 A_JumpIfInventory("TimeToLaunchOctopus",1,"PrepareToHoldAttackGun")
        HPIS A 2 A_Weaponready
        TNT1 A 0 A_JumpIfInventory("TimeToLaunchOctopus",1,"PrepareToHoldAttackGun")
        HPIS A 2 A_Weaponready
        TNT1 A 0 A_JumpIfInventory("TimeToLaunchOctopus",1,"PrepareToHoldAttackGun")
        HPIS A 2 A_Weaponready
        TNT1 A 0 A_JumpIfInventory("TimeToLaunchOctopus",1,"PrepareToHoldAttackGun")
        HPIS A 2 A_Weaponready
        TNT1 A 0 A_JumpIfInventory("TimeToLaunchOctopus",1,"PrepareToHoldAttackGun")
        HPIS A 2 A_Weaponready
        TNT1 A 0 A_JumpIfInventory("TimeToLaunchOctopus",1,"PrepareToHoldAttackGun")
        HPIS A 2 A_Weaponready
        TNT1 A 0 A_JumpIfInventory("TimeToLaunchOctopus",1,"PrepareToHoldAttackGun")
        HPIS A 2 A_Weaponready
        TNT1 A 0 A_JumpIfInventory("TimeToLaunchOctopus",1,"PrepareToHoldAttackGun")
        HPIS A 2 A_Weaponready
        TNT1 A 0 A_JumpIfInventory("TimeToLaunchOctopus",1,"PrepareToHoldAttackGun")
        HPIS A 2 A_Weaponready
        TNT1 A 0 A_JumpIfInventory("TimeToLaunchOctopus",1,"PrepareToHoldAttackGun")
        HPIS A 2 A_Weaponready // 10
        TNT1 A 0 A_JumpIfInventory("TimeToLaunchOctopus",1,"PrepareToHoldAttackGun")
        HPIS A 2 A_Weaponready
        TNT1 A 0 A_JumpIfInventory("TimeToLaunchOctopus",1,"PrepareToHoldAttackGun")
        HPIS A 2 A_Weaponready
        TNT1 A 0 A_JumpIfInventory("TimeToLaunchOctopus",1,"PrepareToHoldAttackGun")
        HPIS A 2 A_Weaponready
        TNT1 A 0 A_JumpIfInventory("TimeToLaunchOctopus",1,"PrepareToHoldAttackGun")
        HPIS A 2 A_Weaponready
        TNT1 A 0 A_JumpIfInventory("TimeToLaunchOctopus",1,"PrepareToHoldAttackGun")
        HPIS A 2 A_Weaponready
        TNT1 A 0 A_JumpIfInventory("TimeToLaunchOctopus",1,"PrepareToHoldAttackGun")
        HPIS A 2 A_Weaponready
        TNT1 A 0 A_JumpIfInventory("TimeToLaunchOctopus",1,"PrepareToHoldAttackGun")
        HPIS A 2 A_Weaponready
        TNT1 A 0 A_JumpIfInventory("TimeToLaunchOctopus",1,"PrepareToHoldAttackGun")
        HPIS A 2 A_Weaponready // 17
        Goto AltFired
    AltFirePrepare:
        WEPA H 1 A_TakeInventory("KharonSwung",1)
        TNT1 A 0 A_TakeInventory("SlashingLikeAGaijin",1)
        TNT1 A 0 A_GiveInventory("ShootingLikeABaka",1)
        WEPA I 1
        goto AltFireGun
    AltFired:
        TNT1 A 0 A_TakeInventory("KharonSwung",1)
        TNT1 A 0 A_TakeInventory("SlashingLikeAGaijin",1)
        TNT1 A 0 A_TakeInventory("ShootingLikeABaka",1)
        TNT1 A 0 A_ClearRefire
        WEPA GFE 1
        Goto Ready

    HenshinCheck:
        TNT1 A 0 A_JumpIfInventory("SuperMeterCounter",100,"Henshin")
        TNT1 A 1
        TNT1 A 0 A_PlaySoundEx("henshin/error2","soundslot5",0,0)
        TNT1 A 0 A_PlaySoundEx("henshin/error","soundslot6",0,0)
        TNT1 A 0 A_PlaySoundEx("*pain","sounslot7",0,0)
        TNT1 A 0 A_Print("Error: Insufficient soul reserve.")
        TNT1 A 0 A_GiveInventory("HenshinCooldown",70)
        TNT1 A 0 A_SetBlend("White",1.0,35)
        TNT1 A 0 A_Recoil(10)
        TNT1 A 0 A_TakeInventory("HenshinActivated",1)
        TNT1 A 0 A_ClearRefire
        goto Fired2
    Henshin:
        TNT1 A 0 //A_TakeInventory("PointsNoSpecials",1)
        WEPA BCD 1
        TNT1 A 1
        TNT1 A 0 A_PlaySoundEx("henshin/click","soundslot6",0,0)
        TNT1 A 4 A_TakeInventory("KharonSwung",1)
        TNT1 A 0 A_PlayWeaponSound("henshin/foley")
        HNSH AABBCDE 1
        HNSH F 15 A_PlaySoundEx("henshin/call","soundslot6",0,0)
        TNT1 A 0 A_PlayWeaponSound("henshin/foley")
        HNSH FGHIIJJKKLL 1
        TNT1 A 0 A_PlaySoundEx("hammer/spark","soundslot5",0,0)
        TNT1 A 0 A_PlayWeaponSound("henshin/foley")
        HNSH MNOOPP 1
        TNT1 A 0 A_PlayWeaponSound("henshin/foley")
        HNSH QRST 1
        TNT1 A 0 A_PlaySoundEx("henshin/donarmor","soundslot6",0,0)
        HNSH UVWX 2
        HNSH Y 2 A_SetBlend("Red",0.1,3)
        HNSH Z 2 A_SetBlend("Red",0.2,3)
        HNSN A 2 A_SetBlend("Red",0.3,3)
        HNSN B 2 A_SetBlend("Red",0.5,3)
        HNSN C 2 A_SetBlend("Red",0.7,3)
        HNSN D 2 A_SetBlend("Red",0.9,3)
        HNSN D 2 A_SetBlend("Red",1.0,70)
        TNT1 A 0 A_GiveInventory("Iron Fist",1)
        TNT1 A 0 A_SelectWeapon("Iron Fist")
        TNT1 A 0 A_SpawnItemEx("IronMaidenRepel",0,0,0,0,0,0,0,48)
        TNT1 A 0 ACS_ExecuteAlways(275,0,WEEB_DEC_CHANGEMUS,0,0)
        TNT1 A 0 A_TakeInventory("AlreadyInLevel",1)
        TNT1 A 0 A_TakeInventory("Kharon + Legion",1)
        TNT1 A 0 A_TakeInventory("Kharon + Frosthammer",1)
        TNT1 A 0 A_TakeInventory("Kharon + Testament",1)
        TNT1 A 0 A_TakeInventory("Kharon + Sabbath",1)
        TNT1 A 0 A_TakeInventory("Kharon + Exodus",1)
        TNT1 A 0 A_TakeInventory("Kharon + Omen",1)
        TNT1 A 0 A_TakeInventory("Kharon + Acacia A-22",1)
        TNT1 A 0 A_TakeInventory("Kharon",1)
        TNT1 A 0 A_TakeInventory("HenshinActivated",1)
        TNT1 A 0 A_TakeInventory("SuperMeterCounter",40)
        TNT1 A 0 A_GiveInventory("IronMaidenArmor2",100)
        TNT1 A 0 A_GiveInventory("InIronMaiden",1)
        TNT1 A 0 A_GiveInventory("IronMaidenSpeed",1)
        TNT1 A 1
        goto Deselect



    DeselectShit:
        TNT1 A 0 A_JumpIfInventory("GotCarronade",1,"ConsiderExodus")
    Consider3:
        TNT1 A 0 A_JumpIfInventory("GotUzi",1,"ConsiderSabbath")
    Consider2:
        TNT1 A 0 A_JumpIfInventory("GotShotgun",1,"ConsiderTestament")
    Consider1:
        TNT1 A 0 A_JumpIfInventory("GotHammer",1,"ConsiderOmen")
    Consider0:
        TNT1 A 0 A_JumpIfInventory("WeebClip",1,"SelectAcacia")
        TNT1 A 0 A_SelectWeapon("Kharon")
        TNT1 AAAAAAAAAAAAAAAAAAAA 0 A_Lower
        TNT1 A 1 A_Lower
        goto Deselect
        
    ConsiderExodus:
        TNT1 A 0 A_JumpIfInventory("WeebRocket",1,"SelectExodus")
        goto Consider3
    ConsiderSabbath:
        TNT1 A 0 A_JumpIfInventory("WeebCell",1,"SelectSabbath")
        goto Consider2
    ConsiderTestament:
        TNT1 A 0 A_JumpIfInventory("WeebShell",1,"SelectTestament")
        goto Consider1
    ConsiderOmen:
        TNT1 A 0 A_JumpIfInventory("HammerCharge",50,"SelectOmen")
        goto Consider0

    SelectExodus:
        TNT1 A 0 A_SelectWeapon("Kharon + Exodus")
        TNT1 AAAAAAAAAAAAAAAAAAAA 0 A_Lower
        TNT1 A 1 A_Lower
        goto Deselect
    SelectSabbath:
        TNT1 A 0 A_SelectWeapon("Kharon + Sabbath")
        TNT1 AAAAAAAAAAAAAAAAAAAA 0 A_Lower
        TNT1 A 1 A_Lower
        goto Deselect
    SelectTestament:
        TNT1 A 0 A_SelectWeapon("Kharon + Testament")
        TNT1 AAAAAAAAAAAAAAAAAAAA 0 A_Lower
        TNT1 A 1 A_Lower
        goto Deselect
    SelectOmen:
        TNT1 A 0 A_SelectWeapon("Kharon + Omen")
        TNT1 AAAAAAAAAAAAAAAAAAAA 0 A_Lower
        TNT1 A 1 A_Lower
        goto Deselect
    SelectAcacia:
        TNT1 A 0 A_SelectWeapon("Kharon + Acacia A-22")
        TNT1 AAAAAAAAAAAAAAAAAAAA 0 A_Lower
        TNT1 A 1 A_Lower
        goto Deselect
    }
}

ACTOR AcaciaCasingSpawner // CALL IT THE ACASIANG. Acaciasing? Acasing? Fuck it, I give up.
{
    Radius 1
    Height 1
    Speed 15
    Projectile
    -SOLID
    +CLIENTSIDEONLY
    States
    {
    Spawn:
        TNT1 A 0
        TNT1 A 1
        TNT1 A 0 A_CustomMissile("AcaciaCasing",-4,17,random(-14000,-10000)/100.00,2,random(500,1500)/100.00)
        Stop
    }
}

ACTOR AcaciaCasing
{ 
    Radius 2
    Height 2
    Speed 5
    Scale 0.125
    BounceFactor 0.55
    WallBounceFactor 0.8
    BounceCount 8
    Gravity 0.8
    Projectile
    -NOGRAVITY
    +NOTELEPORT
    +FLOORCLIP
    +DOOMBOUNCE
    +CLIENTSIDEONLY
    SeeSound "casing/acacia"
    DeathSound "casing/acacia"
    States
    {
    Spawn:
        TNT1 A 0
        TNT1 A 0 A_PlaySound("silence", CHAN_VOICE)
    Spawn1:
        PCSN ABCDEFGH 3
        loop
    Death:
        TNT1 A 0
        TNT1 A 0 A_Jump(255,"Death1","Death2","Death3","Death4")
        TNT1 A 1
        loop
    Death1:
        PCSN B 2100
        PCSN BBBBBBBBBB 1 A_FadeOut(0.1)
        Stop
    Death2:
        PCSN D 2100
        PCSN DDDDDDDDDD 1 A_FadeOut(0.1)
        Stop
    Death3:
        PCSN H 2100
        PCSN HHHHHHHHHH 1 A_FadeOut(0.1)
        Stop
    Death4:
        PCSN I 2100
        PCSN IIIIIIIIII 1 A_FadeOut(0.1)
        Stop
    }
}