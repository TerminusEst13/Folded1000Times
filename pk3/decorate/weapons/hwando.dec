ACTOR Kharon : Weapon
{
    Weapon.SlotNumber 0
    Weapon.KickBack 50
    Weapon.AmmoUse 0
    Weapon.AmmoUse2 0
    Weapon.AmmoGive 0
    Weapon.AmmoType ""
    Weapon.AmmoType2 ""
    Obituary "%k tore %o apart."
    DamageType "kubiuti"
    Weapon.UpSound "weapon/switch"
    Decal BulletChip
    +AXEBLOOD
    +NOALERT
    +NOAUTOFIRE
    +NOEXTREMEDEATH
    +ALT_AMMO_OPTIONAL
    +CHEATNOTWEAPON
    +UNDROPPABLE
    States
    {
    Spawn:
        KATP A -1
        stop
    Ready:
        WEPK ABCD 1 A_WeaponReady
        TNT1 A 2
        TNT1 A 0 A_PlaySoundEx("sword/sheathe","soundslot6",0,0)
        WEPK EFG 1
    Ready2:
        TNT1 A 0 A_JumpIfInventory("WaitingTooLong",0,"IdleAnimation")
        TNT1 A 0 A_JumpIfInventory("HenshinActivated",1,"HenshinCheck")
        WEPK H 1 A_WeaponReady
        loop
    Deselect:
        TNT1 A 0 A_TakeInventory("WaitingTooLong",0x7FFFFFFF)
        TNT1 A 0 A_TakeInventory("SheatheSwung",1)
        TNT1 A 0 A_TakeInventory("KharonSwung",1)
        TNT1 A 0 A_TakeInventory("SlashingLikeAGaijin",1)
        TNT1 A 0 A_TakeInventory("ShootingLikeABaka",1)
        TNT1 AAAAAAAAAAAAAAAAAAAA 0 A_Lower
        TNT1 A 1 A_Lower
        Loop
    Select:
        TNT1 A 0 A_JumpIfInventory("AlreadyInLevel",1,"Select3")
        TNT1 AAAAAAA 0 A_Raise
        TNT1 AA 1 A_Raise
        TNT1 A 0 A_PlayWeaponSound("sword/draw")
        TNT1 A 1 A_Raise
        KTUP ABCDE 1 A_Raise
        KTUP FG 1
        TNT1 A 12 A_GiveInventory("AlreadyInLevel",1)
        TNT1 A 0 A_PlaySoundEx("sword/sheathe","soundslot6",0,0)
        WEPK EFGH 1
        Goto Ready2
    Select2:
        TNT1 A 0 A_PlaySoundEx("sword/sheathe","soundslot6",0,0)
    Select4:
        WEPK EFG 1 A_WeaponReady
        goto Ready2
    Select3:
        TNT1 AAAAAAAAAAAAAAAAAAAA 0 A_Raise
        TNT1 A 1 A_Raise
        loop
    IdleAnimation:
        WEPK HGFE 2 A_WeaponReady
    IdleDecide:
        TNT1 A 0 A_Jump(16,"Idle0")
        TNT1 A 0 A_Jump(255,"Idle1","Idle2","Idle3","Idle4")
        TNT1 A 1
        loop
    Idle0:
        TNT1 A 0
        TNT1 A 0 A_PlaySound("idle/raise")
        IDL0 ABCDEF 2 A_WeaponReady
        TNT1 A 0 A_PlaySound("haelin/idle0")
        IDL0 F 5 A_WeaponReady
        IDL0 F 10 A_WeaponReady
        IDL0 GHI 1 A_WeaponReady
        IDL0 J 10 A_WeaponReady
        IDL0 JKLMNOP 2 A_WeaponReady
        TNT1 A 5 A_WeaponReady
        goto IdleFinish
    Idle1:
        IDL1 ABC 2 A_WeaponReady
        IDL1 C 5 A_WeaponReady
        TNT1 A 0 A_PlaySound("haelin/idle1_1")
        IDL1 C 5 A_WeaponReady
        IDL1 DEFG 2 A_WeaponReady
        IDL1 G 30 A_WeaponReady
        TNT1 A 0 A_PlaySound("haelin/idle1_2")
        IDL1 HIJKLMNO 2 A_WeaponReady
        IDL1 O 12 A_WeaponReady
        TNT1 A 0 A_PlaySound("haelin/idle1_3")
        IDL1 PQRSTUV 2 A_WeaponReady
        IDL1 V 15 A_WeaponReady
        IDL1 WXY 2 A_WeaponReady
        TNT1 A 5 A_WeaponReady
        goto IdleFinish
    Idle2:
        IDL2 ABCCDDEEE 1 A_WeaponReady
        TNT1 A 0 A_PlaySound("idle/spray")
        IDL2 FGHH 2 A_WeaponReady
        TNT1 A 0 A_PlaySound("idle/spray")
        IDL2 FGHH 2 A_WeaponReady
        TNT1 A 0 A_PlaySound("idle/spray")
        IDL2 FGHH 2 A_WeaponReady
        IDL2 HHLLMNOPPQQ 1 A_WeaponReady
        TNT1 A 0 A_PlaySound("idle/rub")
        IDL2 TUVUT 2 A_WeaponReady
        TNT1 A 0 A_PlaySound("idle/rub")
        IDL2 TUVUT 2 A_WeaponReady
        TNT1 A 0 A_PlaySound("idle/rub")
        IDL2 TUVUT 2 A_WeaponReady
        TNT1 A 0 A_PlaySound("idle/rub")
        IDL2 TUVUTT 2 A_WeaponReady
        IDL2 WWWXXYYZ 1 A_WeaponReady
        TNT1 A 5 A_WeaponReady
        goto IdleFinish
    Idle3:
        IDL3 ABCDDEE 1 A_WeaponReady
        IDL3 F 10 A_WeaponReady
        TNT1 A 0 A_PlaySound("idle/shing")
        IDL3 FFGHIJK 1 A_WeaponReady
        IDL3 L 20 A_WeaponReady
        IDL3 MMNO 1 A_WeaponReady
        TNT1 A 5 A_WeaponReady
        goto IdleFinish
    Idle4:
        TNT1 A 2 A_WeaponReady
        IDL4 ABCDDEEFFGG 1 A_WeaponReady
        TNT1 A 0 A_PlaySound("haelin/idleknux")
        IDL4 H 7 A_WeaponReady
        IDL4 IIJJKKLMNOPQRRSSTT 1 A_WeaponReady
        TNT1 A 0 A_PlaySound("haelin/idleknux")
        IDL4 U 7 A_WeaponReady
        IDL4 VVWWXYZ 1 A_WeaponReady
        IDL4 [ 1 A_WeaponReady
        TNT1 A 2 A_WeaponReady
        goto IdleFinish
    IdleFinish:
        TNT1 A 0 A_PlaySoundEx("sword/sheathe","soundslot6",0,0)
        WEPK EFGH 2 A_WeaponReady
        TNT1 A 0 A_TakeInventory("WaitingTooLong",0x7FFFFFFF)
        goto Ready2

    Fire:
        //TNT1 A 0 A_JumpIfInventory("ShootingLikeABaka",1,"FirePrepare")
        TNT1 A 0 A_JumpIfInventory("SlashingLikeAGaijin",1,"FireSequence1")
        TNT1 A 0
        TNT1 A 0 A_GiveInventory("SlashingLikeAGaijin",1)
        goto FireSequence1
    /*FirePrepare:
        WEPK IH 1 A_TakeInventory("ShootingLikeABaka")
        TNT1 A 0 A_GiveInventory("SlashingLikeAGaijin",1)
        goto FireSequence1*/

    FireSequence1: // Right to left
        //TNT1 A 0 A_JumpIfInventory("KharonChargedCounter",5,"KharonSpecialSlash")
        TNT1 A 0 A_PlaySoundEX("sword/swingmed","SoundSlot7")
        TNT1 A 0 A_JumpIfInventory("KharonSwung",1,"FireSequence2")
        TNT1 A 0 A_Jump(255,"FireSequence1A","FireSequence1B","FireSequence1C")
        TNT1 A 0
        TNT1 A 1
        goto FireSequence1+2
    FireSequence1A:
        KATN F 1
        KATN G 1
        TNT1 A 0 A_FireCustomMissile("KharonGuard",0,0) 
        TNT1 A 0 A_FireCustomMissile("KharonAttack3",0,0)
        KATN H 1
        TNT1 A 0 A_FireCustomMissile("KharonGuard",0,0) 
        TNT1 A 0 A_FireCustomMissile("KharonAttack3b",0,0)
        KATN I 1 A_GiveInventory("KharonSwung",1)
        KATN J 1
        TNT1 A 1 A_WeaponReady(WRF_NOSWITCH)
        goto Fired
    FireSequence1B:
        KATN K 1
        KATN L 1
        TNT1 A 0 A_FireCustomMissile("KharonGuard",0,0) 
        TNT1 A 0 A_FireCustomMissile("KharonAttack2",0,0)
        KATN M 1
        TNT1 A 0 A_FireCustomMissile("KharonGuard",0,0) 
        TNT1 A 0 A_FireCustomMissile("KharonAttack2b",0,0)
        KATN N 1 A_GiveInventory("KharonSwung",1)
        KATN O 1
        KATN P 1 A_WeaponReady(WRF_NOSWITCH)
        goto Fired
    FireSequence1C:
        KATN V 1
        KATN W 1
        TNT1 A 0 A_FireCustomMissile("KharonGuard",0,0) 
        TNT1 A 0 A_FireCustomMissile("KharonAttack1",0,0)
        KATN X 1
        TNT1 A 0 A_FireCustomMissile("KharonGuard",0,0) 
        TNT1 A 0 A_FireCustomMissile("KharonAttack1b",0,0)
        KATN Y 1 A_GiveInventory("KharonSwung",1)
        KATN Z 1
        TNT1 A 1 A_WeaponReady(WRF_NOSWITCH)
        goto Fired

    FireSequence2: // Left to right
        TNT1 A 0 A_Jump(255,"FireSequence2A","FireSequence2B","FireSequence2C")
        TNT1 A 0
        TNT1 A 1
        loop
    FireSequence2A:
        KATN A 1
        KATN B 1
        TNT1 A 0 A_FireCustomMissile("KharonGuard",0,0)
        TNT1 A 0 A_FireCustomMissile("KharonAttack1",0,0)
        KATN C 1
        TNT1 A 0 A_FireCustomMissile("KharonGuard",0,0)
        TNT1 A 0 A_FireCustomMissile("KharonAttack1b",0,0)
        KATN D 1 A_TakeInventory("KharonSwung",1)
        KATN E 1
        TNT1 A 1 A_WeaponReady(WRF_NOSWITCH)
        goto Fired
    FireSequence2B:
        KTUP A 1
        KTUP B 1
        TNT1 A 0 A_FireCustomMissile("KharonGuard",0,0)
        TNT1 A 0 A_FireCustomMissile("KharonAttack2",0,0)
        KTUP C 1
        TNT1 A 0 A_FireCustomMissile("KharonGuard",0,0)
        TNT1 A 0 A_FireCustomMissile("KharonAttack2b",0,0)
        KTUP D 1 A_TakeInventory("KharonSwung",1)
        KTUP E 1
        KTUP F 1 A_WeaponReady(WRF_NOSWITCH)
        goto Fired
    FireSequence2C:
        KATN Q 1
        KATN R 1
        TNT1 A 0 A_FireCustomMissile("KharonGuard",0,0)
        TNT1 A 0 A_FireCustomMissile("KharonAttack3",0,0)
        KATN S 1
        TNT1 A 0 A_FireCustomMissile("KharonGuard",0,0)
        TNT1 A 0 A_FireCustomMissile("KharonAttack3b",0,0)
        KATN T 1 A_TakeInventory("KharonSwung",1)
        KATN U 1
        TNT1 A 1 A_WeaponReady(WRF_NOSWITCH)
        goto Fired

    Hold:
        TNT1 A 1 A_WeaponReady(WRF_NOSECONDARY | WRF_NOSWITCH)
        TNT1 A 0 A_JumpIfInventory("DoubleTapBack",1,"RepelWave")
        TNT1 A 0 A_JumpIfInventory("DoubleTapForward",1,"LungeAttack")
        TNT1 A 0 A_JumpIfInventory("DoubleTapLeft",1,"SpinAttackLeft")
        TNT1 A 0 A_JumpIfInventory("DoubleTapRight",1,"SpinAttackRight")
        TNT1 A 0 A_Refire
        TNT1 A 0 A_ClearRefire
        TNT1 A 1 A_WeaponReady(WRF_NOSECONDARY | WRF_NOSWITCH)
        goto Fired2

    Fired:
        TNT1 A 10 A_WeaponReady(WRF_NOSWITCH)
        TNT1 A 0 A_ReFire
    Fired2:
        TNT1 A 25 A_WeaponReady(WRF_NOSWITCH)
        TNT1 A 0 A_TakeInventory("SheatheSwung",1)
        TNT1 A 0 A_TakeInventory("KharonSwung",1)
        TNT1 A 0 A_TakeInventory("SlashingLikeAGaijin",1)
        TNT1 A 0 A_TakeInventory("ShootingLikeABaka",1)
        TNT1 A 0 A_ClearRefire
        Goto Select2



    SpinAttackLeft:
        TNT1 A 1 A_SetAngle(angle+11.25)
        TNT1 A 1 A_SetAngle(angle+22.5)
        TNT1 A 0 A_TakeInventory("SuperMeterCounter",20)
        TNT1 A 0 A_PlayWeaponSound("sword/spinattack")
        TNT1 A 0 A_PlaySoundEx("haelin/attack","soundslot6",0,0)
        TNT1 A 0 A_PlaySoundEx("sword/swingheavy","soundslot7",0,0)
        TNT1 A 0 A_SpawnItemEx("KharonExplosion",0,0,0,0,0,0,0,48)
        TNT1 A 0 A_SpawnItemEx("ShockwaveSpawner",0,0,0,0,0,0,0,32)
        TNT1 A 0 A_CheckFloor("SpinAttackLeftContinue")
        TNT1 A 0 ThrustThingZ(0,24,0,0)
    SpinAttackLeftContinue:
        KSPC G 1 A_SetAngle(angle+45)
        KSPC H 1 A_SetAngle(angle+50)
        KSPC I 1 A_SetAngle(angle+55)
        KSPC J 1 A_SetAngle(angle+55)
        KSPC K 1 A_SetAngle(angle+50)
        KSPC L 1 A_SetAngle(angle+45)
        TNT1 A 1 A_SetAngle(angle+22.5)
        TNT1 A 1 A_SetAngle(angle+11.25)
        goto SpecialAttackFinish

    SpinAttackRight:
        TNT1 A 1 A_SetAngle(angle-11.25)
        TNT1 A 1 A_SetAngle(angle-22.5)
        TNT1 A 0 A_TakeInventory("SuperMeterCounter",20)
        TNT1 A 0 A_PlayWeaponSound("sword/spinattack")
        TNT1 A 0 A_PlaySoundEx("haelin/attack","soundslot6",0,0)
        TNT1 A 0 A_PlaySoundEx("sword/swingheavy","soundslot7",0,0)
        TNT1 A 0 A_SpawnItemEx("KharonExplosion",0,0,0,0,0,0,0,48)
        TNT1 A 0 A_SpawnItemEx("ShockwaveSpawner",0,0,0,0,0,0,0,32)
        TNT1 A 0 A_CheckFloor("SpinAttackRightContinue")
        TNT1 A 0 ThrustThingZ(0,28,0,0)
    SpinAttackRightContinue:
        KSPC M 1 A_SetAngle(angle-45)
        KSPC N 1 A_SetAngle(angle-50)
        KSPC O 1 A_SetAngle(angle-55)
        KSPC P 1 A_SetAngle(angle-55)
        KSPC Q 1 A_SetAngle(angle-50)
        KSPC R 1 A_SetAngle(angle-45)
        TNT1 A 1 A_SetAngle(angle-22.5)
        TNT1 A 1 A_SetAngle(angle-11.25)
        goto SpecialAttackFinish

    LungeAttack:
        TNT1 A 0 A_CheckFloor("LungeAttackRise")
    LungeAttackPrepare:
        TNT1 A 0 A_TakeInventory("SuperMeterCounter",20)
        TNT1 A 0 A_ChangeVelocity(0,0,0,CVF_REPLACE)
        TNT1 A 0 A_ChangeFlag("NOGRAVITY",1)
        TNT1 A 0 A_ChangeFlag("GHOST",1)
        TNT1 A 0 ACS_ExecuteAlways(267,0,WEEB_DEC_FREEZE,0,0)
        TNT1 A 0 A_ChangeFlag("THRUSPECIES",1)
        TNT1 A 1
        TNT1 A 0 A_PlayWeaponSound("sword/beam3")
        TNT1 A 0 A_PlaySoundEx("haelin/attack","soundslot6",0,0)
        TNT1 AA 0 A_SpawnItemEx("RazorWindExplosion",0,0,0,0,0,0,0,48)
        TNT1 A 0 A_SpawnItemEx("RazorWind",0,0,32,125,0,0,0,48)
        TNT1 A 0 A_SpawnItemEx("RazorWind",0,0,32,124,0,0,1,48)
        TNT1 A 0 A_SpawnItemEx("RazorWind",0,0,32,124,0,0,-1,48)
        TNT1 A 0 A_SpawnItemEx("RazorWind",0,0,32,123,0,0,2,48)
        TNT1 A 0 A_SpawnItemEx("RazorWind",0,0,32,123,0,0,-2,48)
        TNT1 A 0 A_SpawnItemEx("RazorWind",0,0,32,122,0,0,3,48)
        TNT1 A 0 A_SpawnItemEx("RazorWind",0,0,32,122,0,0,-3,48)
        TNT1 A 0 A_SpawnItemEx("RazorWind",0,0,32,121,0,0,4,48)
        TNT1 A 0 A_SpawnItemEx("RazorWind",0,0,32,121,0,0,-4,48)
        TNT1 A 0 ThrustThing(angle*256/360,120,1,0)
    LungeAttackContinue:
        KSPC ABCDEF 1
        TNT1 A 1
        TNT1 A 0 A_ChangeVelocity(0,0,0,CVF_REPLACE)
        TNT1 A 0 A_Stop // You'd think this would be redundant.
        TNT1 A 0 ACS_ExecuteAlways(267,0,WEEB_DEC_UNFREEZE,0,0)
        TNT1 A 5 A_SpawnItemEx("TelefragHack",0)
        TNT1 A 0 A_ChangeFlag("NOGRAVITY",0)
        TNT1 A 0 A_ChangeFlag("THRUSPECIES",0)
        TNT1 A 0 A_ChangeFlag("GHOST",0)
        goto SpecialAttackFinish
    LungeAttackRise:
        TNT1 A 1 ThrustThingZ(0,24,0,0)
        goto LungeAttackPrepare

    RepelWave:
        KBLK ABCDEE 1
        KBLK F 15 A_PlayWeaponSound("sword/block")
        KSPC STU 1
        TNT1 A 5
        KSPC VW 1
        TNT1 A 0 A_TakeInventory("SuperMeterCounter",20)
        TNT1 A 0 A_PlayWeaponSound("sword/reflect")
        TNT1 A 0 A_PlaySoundEx("haelin/attack","soundslot6",0,0)
        TNT1 A 0 A_FireCustomMissile("RazorWind2",0,0,0,0,0,0)
        TNT1 A 0 A_FireCustomMissile("RazorWind2",-3,0,0,0,0,0)
        TNT1 A 0 A_FireCustomMissile("RazorWind2",3,0,0,0,0,0)
        TNT1 A 0 A_FireCustomMissile("RazorWind2",-6,0,0,0,0,0)
        TNT1 A 0 A_FireCustomMissile("RazorWind2",6,0,0,0,0,0)
        TNT1 A 0 A_FireCustomMissile("RazorWind2",-9,0,0,0,0,0)
        TNT1 A 0 A_FireCustomMissile("RazorWind2",9,0,0,0,0,0)
        TNT1 A 0 A_FireCustomMissile("RazorWind2",-12,0,0,0,0,0)
        TNT1 A 0 A_FireCustomMissile("RazorWind2",12,0,0,0,0,0)
        TNT1 A 0 A_FireCustomMissile("RazorWind2",-15,0,0,0,0,0)
        TNT1 A 0 A_FireCustomMissile("RazorWind2",15,0,0,0,0,0)
        KSPC XYZ 1
        TNT1 AA 1
        goto SpecialAttackFinish

    SpecialAttackFinish:
        TNT1 A 0 A_TakeInventory("SheatheSwung",1)
        TNT1 A 0 A_TakeInventory("KharonSwung",1)
        TNT1 A 0 A_TakeInventory("SlashingLikeAGaijin",1)
        TNT1 A 0 A_TakeInventory("ShootingLikeABaka",1)
        TNT1 A 0 A_TakeInventory("DoubleTapBack", 1)
        TNT1 A 0 A_TakeInventory("DoubleTapLeft", 1)
        TNT1 A 0 A_TakeInventory("DoubleTapRight", 1)
        TNT1 A 0 A_TakeInventory("DoubleTapForward", 1)
        goto Fired2

    HenshinCheck:
        TNT1 A 0 A_JumpIfInventory("SuperMeterCounter",100,"Henshin")
        TNT1 A 1
        TNT1 A 0 A_PlaySoundEx("henshin/error2","soundslot5",0,0)
        TNT1 A 0 A_PlaySoundEx("henshin/error","soundslot6",0,0)
        TNT1 A 0 A_PlaySoundEx("*pain","sounslot7",0,0)
        TNT1 A 0 A_SetBlend("White",1.0,35)
        TNT1 A 0 A_Recoil(10)
        TNT1 A 0 A_TakeInventory("HenshinActivated",1)
        TNT1 A 0 A_ClearRefire
        goto Fired2
    Henshin:
        WEPK GFE 1
        TNT1 A 1
        TNT1 A 0 A_PlaySoundEx("henshin/click","soundslot6",0,0)
        TNT1 A 4 A_TakeInventory("KharonSwung",1)
        TNT1 A 0 A_TakeInventory("SheatheSwung",1)
        TNT1 A 0 A_PlayWeaponSound("henshin/foley")
        HNSH AABBCDE 1
        HNSH F 15 A_PlaySoundEx("henshin/call","soundslot6",0,0)
        TNT1 A 0 A_PlayWeaponSound("henshin/foley")
        HNSH FGHIIJJKKLL 1
        TNT1 A 0 A_PlaySoundEx("hammer/spark","soundslot5",0,0)
        TNT1 A 0 A_PlayWeaponSound("henshin/foley")
        HNSH MNOOPP 1
        TNT1 A 0 A_PlayWeaponSound("henshin/foley")
        HNSH QRST 1
        TNT1 A 0 A_PlaySoundEx("henshin/donarmor","soundslot6",0,0)
        HNSH UVWX 2
        HNSH Y 2 A_SetBlend("Red",0.1,3)
        HNSH Z 2 A_SetBlend("Red",0.2,3)
        HNSN A 2 A_SetBlend("Red",0.3,3)
        HNSN B 2 A_SetBlend("Red",0.5,3)
        HNSN C 2 A_SetBlend("Red",0.7,3)
        HNSN D 2 A_SetBlend("Red",0.9,3)
        HNSN D 2 A_SetBlend("Red",1.0,70)
        TNT1 A 0 A_GiveInventory("Iron Fist",1)
        TNT1 A 0 A_SelectWeapon("Iron Fist")
        TNT1 A 0 A_SpawnItemEx("IronMaidenRepel",0,0,0,0,0,0,0,48)
        TNT1 A 0 ACS_ExecuteAlways(275,0,WEEB_DEC_CHANGEMUS,0,0)
        TNT1 A 0 A_TakeInventory("AlreadyInLevel",1)
        TNT1 A 0 A_TakeInventory("Kharon + Legion",1)
        TNT1 A 0 A_TakeInventory("Kharon + Testament",1)
        TNT1 A 0 A_TakeInventory("Kharon + Sabbath",1)
        TNT1 A 0 A_TakeInventory("Kharon + Exodus",1)
        TNT1 A 0 A_TakeInventory("Kharon + Omen",1)
        TNT1 A 0 A_TakeInventory("Kharon + Acacia A-22",1)
        TNT1 A 0 A_TakeInventory("Kharon",1)
        TNT1 A 0 A_TakeInventory("HenshinActivated",1)
        TNT1 A 0 A_TakeInventory("SuperMeterCounter",40)
        TNT1 A 0 A_GiveInventory("IronMaidenArmor2",100)
        TNT1 A 0 A_GiveInventory("InIronMaiden",1)
        TNT1 A 0 A_GiveInventory("IronMaidenSpeed",1)
        TNT1 A 1
        goto Deselect

    AltFire:
        TNT1 A 0 A_JumpIfInventory("SlashingLikeAGaijin",1,"AltFirePrepare")
        TNT1 A 0 A_JumpIfInventory("ShootingLikeABaka",1,"AltFirePrepare")
        TNT1 A 0 A_PlayWeaponSound("sheathe/lower")
        WEPK HIJ 1
        goto AltHold
    AltFirePrepare:
        TNT1 A 0 A_TakeInventory("KharonSwung",1)
        //TNT1 A 0 A_TakeInventory("SlashingLikeAGaijin",1)
        TNT1 A 0 A_GiveInventory("ShootingLikeABaka",1)
        //TNT1 A 0 A_PlayWeaponSound("sheathe/lower")
        goto AltHold
    AltHold:
        TNT1 A 1
        TNT1 A 0 A_Refire
        TNT1 A 0 A_ClearRefire
        TNT1 A 0 A_PlayWeaponSound("sheathe/swing")
        TNT1 A 0 A_JumpIfInventory("SheatheSwung",1,"AltAttack2")
        TNT1 A 0 A_JumpIfInventory("ShootingLikeABaka",1,"AltAttack1")
        TNT1 A 0 A_FireCustomMissile("KharonGuardSpecial",-35,0,-35)
        TNT1 A 0 A_FireCustomMissile("KharonGuardSpecial",-20,0,-20)
        TNT1 A 0 A_FireCustomMissile("KharonGuardSpecial",0,0)
        TNT1 A 0 A_FireCustomMissile("KharonGuardSpecial",20,0,20)
        TNT1 A 0 A_FireCustomMissile("KharonGuardSpecial",35,0,35)
        SHTH A 1
        TNT1 A 0 A_FireCustomMissile("KharonGuardSpecial",-35,0,-35)
        TNT1 A 0 A_FireCustomMissile("KharonGuardSpecial",-20,0,-20)
        TNT1 A 0 A_FireCustomMissile("KharonGuardSpecial",0,0)
        TNT1 A 0 A_FireCustomMissile("KharonGuardSpecial",20,0,20)
        TNT1 A 0 A_FireCustomMissile("KharonGuardSpecial",35,0,35)
        TNT1 A 0 A_FireCustomMissile("KharonSheatheSmack",0,0)
        SHTH B 1
        TNT1 A 0 A_FireCustomMissile("KharonGuardSpecial",-35,0,-35)
        TNT1 A 0 A_FireCustomMissile("KharonGuardSpecial",-20,0,-20)
        TNT1 A 0 A_FireCustomMissile("KharonGuardSpecial",0,0)
        TNT1 A 0 A_FireCustomMissile("KharonGuardSpecial",20,0,20)
        TNT1 A 0 A_FireCustomMissile("KharonGuardSpecial",35,0,35)
        SHTH C 1
        TNT1 A 0 A_FireCustomMissile("KharonGuardSpecial",-35,0,-35)
        TNT1 A 0 A_FireCustomMissile("KharonGuardSpecial",-20,0,-20)
        TNT1 A 0 A_FireCustomMissile("KharonGuardSpecial",0,0)
        TNT1 A 0 A_FireCustomMissile("KharonGuardSpecial",20,0,20)
        TNT1 A 0 A_FireCustomMissile("KharonGuardSpecial",35,0,35)
        SHTH D 1
        TNT1 A 1 A_WeaponReady(WRF_NOSWITCH)
        TNT1 A 0 A_JumpIfInventory("SlashingLikeAGaijin",1,"AltFired2")
    AltFired:
        TNT1 A 0 A_GiveInventory("ShootingLikeABaka",1)
        TNT1 A 35 A_WeaponReady(WRF_NOSWITCH)
        TNT1 A 0 A_TakeInventory("SheatheSwung",1)
        TNT1 A 0 A_TakeInventory("KharonSwung",1)
        TNT1 A 0 A_TakeInventory("SlashingLikeAGaijin",1)
        TNT1 A 0 A_TakeInventory("ShootingLikeABaka",1)
        TNT1 A 0 A_ClearRefire
        TNT1 A 1
        Goto Select4
    AltFired2:
        TNT1 A 35 A_WeaponReady(WRF_NOSWITCH)
        TNT1 A 0 A_TakeInventory("SheatheSwung",1)
        TNT1 A 0 A_TakeInventory("KharonSwung",1)
        TNT1 A 0 A_TakeInventory("SlashingLikeAGaijin",1)
        TNT1 A 0 A_TakeInventory("ShootingLikeABaka",1)
        TNT1 A 0 A_ClearRefire
        TNT1 A 1
        Goto Select2
    AltAttack1:
        TNT1 A 0 A_PlayWeaponSound("sheathe/swing")
        TNT1 A 0 A_FireCustomMissile("KharonGuardSpecial",-35,0,-35)
        TNT1 A 0 A_FireCustomMissile("KharonGuardSpecial",-20,0,-20)
        TNT1 A 0 A_FireCustomMissile("KharonGuardSpecial",0,0)
        TNT1 A 0 A_FireCustomMissile("KharonGuardSpecial",20,0,20)
        TNT1 A 0 A_FireCustomMissile("KharonGuardSpecial",35,0,35)
        SHTH E 1
        TNT1 A 0 A_FireCustomMissile("KharonGuardSpecial",-35,0,-35)
        TNT1 A 0 A_FireCustomMissile("KharonGuardSpecial",-20,0,-20)
        TNT1 A 0 A_FireCustomMissile("KharonGuardSpecial",0,0)
        TNT1 A 0 A_FireCustomMissile("KharonGuardSpecial",20,0,20)
        TNT1 A 0 A_FireCustomMissile("KharonGuardSpecial",35,0,35)
        SHTH F 1
        TNT1 A 0 A_FireCustomMissile("KharonGuardSpecial",-35,0,-35)
        TNT1 A 0 A_FireCustomMissile("KharonGuardSpecial",-20,0,-20)
        TNT1 A 0 A_FireCustomMissile("KharonGuardSpecial",0,0)
        TNT1 A 0 A_FireCustomMissile("KharonGuardSpecial",20,0,20)
        TNT1 A 0 A_FireCustomMissile("KharonGuardSpecial",35,0,35)
        TNT1 A 0 A_FireCustomMissile("KharonSheatheSmack2",0,0)
        SHTH G 1
        TNT1 A 0 A_FireCustomMissile("KharonGuardSpecial",-35,0,-35)
        TNT1 A 0 A_FireCustomMissile("KharonGuardSpecial",-20,0,-20)
        TNT1 A 0 A_FireCustomMissile("KharonGuardSpecial",0,0)
        TNT1 A 0 A_FireCustomMissile("KharonGuardSpecial",20,0,20)
        TNT1 A 0 A_FireCustomMissile("KharonGuardSpecial",35,0,35)
        SHTH H 1 A_GiveInventory("SheatheSwung",1)
        SHTH I 1
        goto AltFired2
    AltAttack2:
        TNT1 A 0 A_PlayWeaponSound("sheathe/swing")
        TNT1 A 0 A_FireCustomMissile("KharonGuardSpecial",-35,0,-35)
        TNT1 A 0 A_FireCustomMissile("KharonGuardSpecial",-20,0,-20)
        TNT1 A 0 A_FireCustomMissile("KharonGuardSpecial",0,0)
        TNT1 A 0 A_FireCustomMissile("KharonGuardSpecial",20,0,20)
        TNT1 A 0 A_FireCustomMissile("KharonGuardSpecial",35,0,35)
        SHTH J 1
        TNT1 A 0 A_FireCustomMissile("KharonGuardSpecial",-35,0,-35)
        TNT1 A 0 A_FireCustomMissile("KharonGuardSpecial",-20,0,-20)
        TNT1 A 0 A_FireCustomMissile("KharonGuardSpecial",0,0)
        TNT1 A 0 A_FireCustomMissile("KharonGuardSpecial",20,0,20)
        TNT1 A 0 A_FireCustomMissile("KharonGuardSpecial",35,0,35)
        SHTH K 1
        TNT1 A 0 A_FireCustomMissile("KharonGuardSpecial",-35,0,-35)
        TNT1 A 0 A_FireCustomMissile("KharonGuardSpecial",-20,0,-20)
        TNT1 A 0 A_FireCustomMissile("KharonGuardSpecial",0,0)
        TNT1 A 0 A_FireCustomMissile("KharonGuardSpecial",20,0,20)
        TNT1 A 0 A_FireCustomMissile("KharonGuardSpecial",35,0,35)
        TNT1 A 0 A_FireCustomMissile("KharonSheatheSmack2",0,0)
        SHTH L 1
        TNT1 A 0 A_FireCustomMissile("KharonGuardSpecial",-35,0,-35)
        TNT1 A 0 A_FireCustomMissile("KharonGuardSpecial",-20,0,-20)
        TNT1 A 0 A_FireCustomMissile("KharonGuardSpecial",0,0)
        TNT1 A 0 A_FireCustomMissile("KharonGuardSpecial",20,0,20)
        TNT1 A 0 A_FireCustomMissile("KharonGuardSpecial",35,0,35)
        SHTH M 1 A_TakeInventory("SheatheSwung",1)
        SHTH N 1
        goto AltFired2
    /*KharonSpecialSlash:
        TNT1 A 0 A_PlaySoundEx("sword/swingheavy","soundslot7",0,0)
        TNT1 A 1
        KTCH A 1
        KTCH B 1
        TNT1 A 0 A_TakeInventory("KharonChargedCounter",5)
        TNT1 A 0 A_FireCustomMissile("KharonRipAttack",-30,0,-30)
        TNT1 A 0 A_FireCustomMissile("KharonRipAttack",-20,0,-20)
        TNT1 A 0 A_FireCustomMissile("KharonRipAttack",-10,0,-10)
        TNT1 A 0 A_FireCustomMissile("KharonRipAttack",0,0)
        TNT1 A 0 A_FireCustomMissile("KharonRipAttack",10,0,10)
        TNT1 A 0 A_FireCustomMissile("KharonRipAttack",20,0,20)
        TNT1 A 0 A_FireCustomMissile("KharonRipAttack",30,0,30)
        KTCH C 1
        KTCH D 1
        KTCH E 1
        KTCH F 1
        TNT1 A 1
        goto Fired*/
    }
}

actor KharonGuard : FastProjectile
{
    Radius 16
    Height 16
    Health 2
    Speed 48
    Damage (10)
    DamageType "GuardMove"
    DamageFactor "kubiuti", 0.0
    DamageFactor "HaltHammerzeit", 0.0
    DamageFactor "GuardMove", 0.0
    DamageFactor "KubiRangeAttack", 0.0
    DamageFactor "IronFistDamage", 0.0
    Species "Player"
    Projectile
    +THRUSPECIES
    +SHOOTABLE
    +NOBLOOD
    +DONTSPLASH
    +NOCLIP
    -NOBLOCKMAP
    States
    {
    Spawn:
        TNT1 A 2
        Stop
    Death:
        TNT1 A 0
        TNT1 A 1
        Stop
    XDeath:
    Crash:
        TNT1 A 0
        TNT1 A 0 A_PlaySound("sword/shielddefense")
        TNT1 A 1
        Stop
    }
}

actor KharonGuard2 : KharonGuard
{
    Damage (10)
    DamageType "GuardMove"
    States
    {
    Death:
        TNT1 A 1
        TNT1 A 0 //A_GiveToTarget("StopTheBlock",1)
        Stop
    }
}

actor KharonGuard3 : KharonGuard2
{
    Health 0x7FFFFFFF
    Radius 96
    Height 96
    -SOLID
}

actor KharonGuardSpecial : FastProjectile
{
    Radius 32
    Height 32
    Health 2
    Speed 64
    Damage (10)
    PainChance 255
    DamageType "GuardMove"
    RadiusDamageFactor 0.0
    DamageFactor "kubiuti", 0.0
    DamageFactor "HaltHammerzeit", 0.0
    DamageFactor "GuardMove", 0.0
    DamageFactor "KubiRangeAttack", 0.0
    DamageFactor "IronFistDamage", 0.0
    Species "Player"
    Projectile
    +THRUSPECIES
    +SHOOTABLE
    +NOBLOOD
    +BLOODLESSIMPACT
    +DONTSPLASH
    +NODAMAGE
    -NOBLOCKMAP
    States
    {
    Spawn:
        TNT1 A 2
        Stop
    Pain:
        TNT1 A 0
        TNT1 A 0 A_Stop
        TNT1 A 0 A_PlaySound("sword/shielddefense")
        TNT1 A 0 A_GiveToTarget("SuperMeterCounter",1)
        TNT1 A 0 A_GiveToTarget("HyperComboCounter",2)//("KharonChargedCounter",1)
        TNT1 A 1
        stop
    Death:
        TNT1 A 0
        TNT1 A 1
        Stop
    }
}

ACTOR KharonAttack1 : FastProjectile
{
    Radius 1
    Height 1
    Speed 112
    Damage (30)
    DamageType "Kubiuti"
    Species "Player"
    Projectile
    +BLOODSPLATTER
    +THRUGHOST
    +THRUSPECIES
    +NOTIMEFREEZE
    +NOEXTREMEDEATH
    Decal KharonSlash1
    States
    {
    Spawn:
        TNT1 A 1
        Stop
    XDeath: // When hitting a bleeding actor.
        TNT1 A 0
        TNT1 A 1
        TNT1 A 0 A_PlaySound("sword/hit")
        TNT1 A 0 A_GiveToTarget("MidCombat",75)
        TNT1 A 0 A_GiveToTarget("HyperComboCounter",3)
        TNT1 A 0 A_SpawnItemEx("CuttingBlood",0,0,0,0,0,0,0,32)
        TNT1 A 1
        stop
    Death: // When hitting a wall.
        TNT1 A 0
        TNT1 A 1
        TNT1 AAA 0 A_SpawnItemEx("SwordSmoke",0,0,0,0,random(-30,30)/100.00,random(10,35)/100.00,random(0,180),160)
        TNT1 A 0 A_SpawnItemEx("SwordPuff",0,0,0,0,0,0,0,32)
        TNT1 A 1
        Stop
    Crash: // When hitting a +NOBLOOD actor.
        TNT1 A 0
        TNT1 A 1
        TNT1 A 0 A_PlaySound("sword/shielddefense")
        TNT1 A 0 A_GiveToTarget("MidCombat",75)
        TNT1 A 0 A_GiveToTarget("HyperComboCounter",3)
        TNT1 A 1
        stop
    }
}

actor KharonAttack1b : KharonAttack1 { Decal "" }
actor KharonAttack2 : KharonAttack1 { Decal KharonSlash2 }
actor KharonAttack2b : KharonAttack1 { Decal "" }
actor KharonAttack3 : KharonAttack1 { Decal KharonSlash3 }
actor KharonAttack3b : KharonAttack1 { Decal "" }

actor KharonSheatheSmack : FastProjectile
{
    Radius 16
    Height 6
    Speed 112
    Damage (20)
    DamageType "KharonSheathe"
    Species "Player"
    Projectile
    +THRUSPECIES
    +THRUGHOST
    +NOTIMEFREEZE
    //+SHOOTABLE
    //+REFLECTIVE
    +NOEXTREMEDEATH
    +BLOODSPLATTER
    -NOBLOCKMAP
    States
    {
    Spawn:
        TNT1 A 1
        Stop
    XDeath:
        TNT1 A 0
        TNT1 A 0 A_GiveToTarget("MidCombat",75)
        TNT1 A 0 A_GiveToTarget("HyperComboCounter",3)
    Death:
        TNT1 A 0
        TNT1 A 0 A_PlaySound("sheathe/reflect")
        TNT1 AAAA 0 A_SpawnItemEx("SwordSmoke",0,0,0,0,random(-30,30)/100.00,random(10,35)/100.00,random(0,180),160)
        TNT1 A 1
        Stop
    Crash:
        TNT1 A 0
        TNT1 A 0 A_PlaySound("sheathe/reflect")
        TNT1 A 1
        Stop
    }
}

actor KharonSheatheSmack2 : KharonSheatheSmack
{
    Damage (40)
    States
    {
    XDeath:
        TNT1 A 0
        TNT1 A 0 A_GiveToTarget("MidCombat",75)
        TNT1 A 0 A_GiveToTarget("HyperComboCounter",6)
        goto Death
    }
}

ACTOR KharonRipAttack : FastProjectile
{
    Radius 8
    Height 2
    Speed 90
    Damage (10)
    DamageType "Kubiuti"
    Species "Player"
    Projectile
    +BLOODSPLATTER
    +THRUGHOST
    +THRUSPECIES
    +NOTIMEFREEZE
    +NOEXTREMEDEATH
    +RIPPER
    States
    {
    Spawn:
        TNT1 A 3
        Stop
    Death:
        TNT1 A 0
        TNT1 A 1
        TNT1 A 0 A_PlayWeaponSound("sword/wallpuff")
        TNT1 A 1
        Stop
    }
}