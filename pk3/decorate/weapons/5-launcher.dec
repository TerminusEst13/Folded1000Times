// EXODUS
//   A hand-held grenade launcher and small howitzer, the Exodus packs a supreme
// punch and can devastate anything that it hits. Designed as a lightweight
// siege weapon against vehicles and barricades, the tiny size more suitable for
// a sidearm does very little to assist with its accuracy or range. Yet, despite
// that, the rapid-loading drum magazine and the potent (if not volatile) load
// within the shells more than make up for its shortcomings with overwhelming
// force, and many angels have taken to outright ignoring more capable and
// reliable guns in favor of simply carrying around an Exodus simply for the
// shock factor.
//   The basis for the design lies in intentionally taking after Earthly archaic
// cannons--with no backwards vent or exhaust to take care of the flames, the
// shell has only one way to go. The drum loads a small packet of gunpowder and
// shell directly into the barrel, and on pulling the trigger the piston jams in
// and ignites the powder. With the force shoving it out and slinging back on
// the poor arm on the user, the shell flies out with massive velocity and tears
// into anything in its path before detonating in a massively catastrophic
// explosion.

//   Though the chemicals and powders inside of the shells are a mystery, their
// power simply can't be denied. Effortlessly consuming hordes in one shot, even
// enemies normally immune to fiery blasts still suffer under the power of the
// Exodus. The blast radius is wide enough to envelop people numerous meters
// away from the impact zone, and for them...death is usually guaranteed.

actor "Kharon + Exodus" : Kharon
{
    -CHEATNOTWEAPON
    Inventory.PickupMessage "You got the Exodus handheld grenade laucher!"
    Weapon.AmmoUse2 1
    Weapon.AmmoGive2 10
    Weapon.AmmoType2 "WeebRocket"
    Weapon.SlotNumber 5
    States
    {
    Spawn:
        GLAU A -1
        Stop
    Ready:
        TNT1 A 0 A_JumpIfInventory("WaitingTooLong",0,"IdleAnimation")
        TNT1 A 0 A_JumpIfInventory("HenshinActivated",1,"HenshinCheck")
        WEPE A 1 A_WeaponReady
        Loop
    Select:
        TNT1 A 0 A_JumpIfInventory("AlreadyInLevel",1,"Select3")
        TNT1 AAAAAAA 0 A_Raise
        TNT1 AA 1 A_Raise
        WAC1 C 0 A_PlayWeaponSound("sword/draw")
        TNT1 A 1 A_Raise
        KTUP ABCDE 1 A_Raise
        KTUP FG 1
        TNT1 A 12 A_GiveInventory("AlreadyInLevel",1)
        WEPE DCBA 1
        Goto Ready
    Select2:
        WEPE DCB 1 A_WeaponReady
        goto Ready
    Select3:
        TNT1 AAAAAAAAAAAAAAAAAAAA 0 A_Raise
        TNT1 A 1 A_Raise
        loop
    IdleAnimation:
        WEPE ABCD 2 A_WeaponReady
    IdleDecide:
        TNT1 A 0 A_Jump(16,"Idle0")
        TNT1 A 0 A_Jump(255,"Idle1","Idle2","Idle3","Idle4")
        TNT1 A 1
        loop
    Idle0:
        TNT1 A 0
        TNT1 A 0 A_PlaySound("idle/raise")
        IDL0 ABCDEF 2 A_WeaponReady
        TNT1 A 0 A_PlaySound("haelin/idle0")
        IDL0 F 5 A_WeaponReady
        IDL0 F 10 A_WeaponReady
        IDL0 GHI 1 A_WeaponReady
        IDL0 J 10 A_WeaponReady
        IDL0 JKLMNOP 2 A_WeaponReady
        TNT1 A 5 A_WeaponReady
        goto IdleFinish
    Idle1:
        IDL1 ABC 2 A_WeaponReady
        IDL1 C 5 A_WeaponReady
        TNT1 A 0 A_PlaySound("haelin/idle1_1")
        IDL1 C 5 A_WeaponReady
        IDL1 DEFG 2 A_WeaponReady
        IDL1 G 30 A_WeaponReady
        TNT1 A 0 A_PlaySound("haelin/idle1_2")
        IDL1 HIJKLMNO 2 A_WeaponReady
        IDL1 O 12 A_WeaponReady
        TNT1 A 0 A_PlaySound("haelin/idle1_3")
        IDL1 PQRSTUV 2 A_WeaponReady
        IDL1 V 15 A_WeaponReady
        IDL1 WXY 2 A_WeaponReady
        TNT1 A 5 A_WeaponReady
        goto IdleFinish
    Idle2:
        IDL2 ABCCDDEEE 1 A_WeaponReady
        TNT1 A 0 A_PlaySound("idle/spray")
        IDL2 FGHH 2 A_WeaponReady
        TNT1 A 0 A_PlaySound("idle/spray")
        IDL2 FGHH 2 A_WeaponReady
        TNT1 A 0 A_PlaySound("idle/spray")
        IDL2 FGHH 2 A_WeaponReady
        IDL2 HHLLMNOPPQQ 1 A_WeaponReady
        TNT1 A 0 A_PlaySound("idle/rub")
        IDL2 TUVUT 2 A_WeaponReady
        TNT1 A 0 A_PlaySound("idle/rub")
        IDL2 TUVUT 2 A_WeaponReady
        TNT1 A 0 A_PlaySound("idle/rub")
        IDL2 TUVUT 2 A_WeaponReady
        TNT1 A 0 A_PlaySound("idle/rub")
        IDL2 TUVUTT 2 A_WeaponReady
        IDL2 WWWXXYYZ 1 A_WeaponReady
        TNT1 A 5 A_WeaponReady
        goto IdleFinish
    Idle3:
        IDL3 ABCDDEE 1 A_WeaponReady
        IDL3 F 10 A_WeaponReady
        TNT1 A 0 A_PlaySound("idle/shing")
        IDL3 FFGHIJK 1 A_WeaponReady
        IDL3 L 20 A_WeaponReady
        IDL3 MMNO 1 A_WeaponReady
        TNT1 A 5 A_WeaponReady
        goto IdleFinish
    Idle4:
        TNT1 A 2 A_WeaponReady
        IDL4 ABCDDEEFFGG 1 A_WeaponReady
        TNT1 A 0 A_PlaySound("haelin/idleknux")
        IDL4 H 7 A_WeaponReady
        IDL4 IIJJKKLMNOPQRRSSTT 1 A_WeaponReady
        TNT1 A 0 A_PlaySound("haelin/idleknux")
        IDL4 U 7 A_WeaponReady
        IDL4 VVWWXYZ 1 A_WeaponReady
        IDL4 [ 1 A_WeaponReady
        TNT1 A 2 A_WeaponReady
        goto IdleFinish
    IdleFinish:
        WEPE DCBA 2 A_WeaponReady
        TNT1 A 0 A_TakeInventory("WaitingTooLong",0x7FFFFFFF)
        goto Ready



    Fire:
        TNT1 A 0 A_JumpIfInventory("ShootingLikeABaka",1,"FirePrepare")
        TNT1 A 0 A_JumpIfInventory("SlashingLikeAGaijin",1,"FireSequence1")
        WEPE BCD 1
        TNT1 A 0 A_GiveInventory("SlashingLikeAGaijin",1)
        goto FireSequence1
    FirePrepare:
        WEPE IH 1 A_TakeInventory("ShootingLikeABaka")
        TNT1 A 0 A_GiveInventory("SlashingLikeAGaijin",1)
        goto FireSequence1

    FireSequence1: // Right to left
        TNT1 A 0 A_PlaySoundEX("sword/swingmed","SoundSlot7")
        TNT1 A 0 A_JumpIfInventory("KharonSwung",1,"FireSequence2")
        TNT1 A 0 A_Jump(255,"FireSequence1A","FireSequence1B","FireSequence1C")
        TNT1 A 0
        TNT1 A 1
        goto FireSequence1+2
    FireSequence1A:
        KATN F 1
        KATN G 1
        TNT1 A 0 A_FireCustomMissile("KharonGuard",0,0) 
        TNT1 A 0 A_FireCustomMissile("KharonAttack3",0,0)
        KATN H 1
        TNT1 A 0 A_FireCustomMissile("KharonGuard",0,0) 
        TNT1 A 0 A_FireCustomMissile("KharonAttack3b",0,0)
        KATN I 1 A_GiveInventory("KharonSwung",1)
        KATN J 1
        TNT1 A 1 A_WeaponReady(WRF_NOSWITCH)
        goto Fired
    FireSequence1B:
        KATN K 1
        KATN L 1
        TNT1 A 0 A_FireCustomMissile("KharonGuard",0,0) 
        TNT1 A 0 A_FireCustomMissile("KharonAttack2",0,0)
        KATN M 1
        TNT1 A 0 A_FireCustomMissile("KharonGuard",0,0) 
        TNT1 A 0 A_FireCustomMissile("KharonAttack2b",0,0)
        KATN N 1 A_GiveInventory("KharonSwung",1)
        KATN O 1
        KATN P 1 A_WeaponReady(WRF_NOSWITCH)
        goto Fired
    FireSequence1C:
        KATN V 1
        KATN W 1
        TNT1 A 0 A_FireCustomMissile("KharonGuard",0,0) 
        TNT1 A 0 A_FireCustomMissile("KharonAttack1",0,0)
        KATN X 1
        TNT1 A 0 A_FireCustomMissile("KharonGuard",0,0) 
        TNT1 A 0 A_FireCustomMissile("KharonAttack1b",0,0)
        KATN Y 1 A_GiveInventory("KharonSwung",1)
        KATN Z 1
        TNT1 A 1 A_WeaponReady(WRF_NOSWITCH)
        goto Fired

    FireSequence2: // Left to right
        TNT1 A 0 A_Jump(255,"FireSequence2A","FireSequence2B","FireSequence2C")
        TNT1 A 0
        TNT1 A 1
        loop
    FireSequence2A:
        KATN A 1
        KATN B 1
        TNT1 A 0 A_FireCustomMissile("KharonGuard",0,0) 
        TNT1 A 0 A_FireCustomMissile("KharonAttack1",0,0)
        KATN C 1
        TNT1 A 0 A_FireCustomMissile("KharonGuard",0,0) 
        TNT1 A 0 A_FireCustomMissile("KharonAttack1b",0,0)
        KATN D 1 A_TakeInventory("KharonSwung",1)
        KATN E 1
        TNT1 A 1 A_WeaponReady(WRF_NOSWITCH)
        goto Fired
    FireSequence2B:
        KTUP A 1
        KTUP B 1
        TNT1 A 0 A_FireCustomMissile("KharonGuard",0,0) 
        TNT1 A 0 A_FireCustomMissile("KharonAttack2",0,0)
        KTUP C 1
        TNT1 A 0 A_FireCustomMissile("KharonGuard",0,0) 
        TNT1 A 0 A_FireCustomMissile("KharonAttack2b",0,0)
        KTUP D 1 A_TakeInventory("KharonSwung",1)
        KTUP E 1
        KTUP F 1 A_WeaponReady(WRF_NOSWITCH)
        goto Fired
    FireSequence2C:
        KATN Q 1
        KATN R 1
        TNT1 A 0 A_FireCustomMissile("KharonGuard",0,0) 
        TNT1 A 0 A_FireCustomMissile("KharonAttack3",0,0)
        KATN S 1
        TNT1 A 0 A_FireCustomMissile("KharonGuard",0,0) 
        TNT1 A 0 A_FireCustomMissile("KharonAttack3b",0,0)
        KATN T 1 A_TakeInventory("KharonSwung",1)
        KATN U 1
        TNT1 A 1 A_WeaponReady(WRF_NOSWITCH)
        goto Fired

    Hold:
        TNT1 A 1 A_WeaponReady(WRF_NOSECONDARY | WRF_NOSWITCH)
        TNT1 A 0 A_JumpIfInventory("DoubleTapBack",1,"RepelWave")
        TNT1 A 0 A_JumpIfInventory("DoubleTapForward",1,"LungeAttack")
        TNT1 A 0 A_JumpIfInventory("DoubleTapLeft",1,"SpinAttackLeft")
        TNT1 A 0 A_JumpIfInventory("DoubleTapRight",1,"SpinAttackRight")
        TNT1 A 0 A_GiveInventory("SwordAttack",1)
        TNT1 A 0 A_Refire
        TNT1 A 0 A_ClearRefire
        TNT1 A 1 A_WeaponReady(WRF_NOSECONDARY | WRF_NOSWITCH)
        goto Fired2

    Fired:
        TNT1 A 10 A_WeaponReady(WRF_NOSWITCH)
        TNT1 A 0 A_ReFire
    Fired2:
        TNT1 A 0 A_JumpIfInventory("SwordAttack",1,"HoldAttack")
        TNT1 A 0 A_TakeInventory("SwordAttack",0x7FFFFFFF)
        TNT1 A 5 A_WeaponReady(WRF_NOSWITCH)
        TNT1 A 0 A_TakeInventory("KharonSwung",1)
        TNT1 A 0 A_TakeInventory("SlashingLikeAGaijin",1)
        TNT1 A 0 A_TakeInventory("ShootingLikeABaka",1)
        TNT1 A 0 A_ClearRefire
        Goto Select2
    HoldAttack:
        //TNT1 A 0 A_Recoil(-2)
        TNT1 A 0 A_SetPitch(pitch-0.5)
        TNT1 A 0 A_PlaySoundEx("sword/swingheavy","soundslot7",0,0)
        KATH A 1
        TNT1 A 0 A_SetPitch(pitch-1)
        TNT1 A 0 A_TakeInventory("SwordAttack",0x7FFFFFF)
        KATH B 1
        TNT1 A 0 A_FireCustomMissile("KharonGuard",0,0)
        TNT1 A 0 A_FireCustomMissile("KharonAttackHeavy",0,0)
        KATH C 1
        TNT1 A 0 A_SetPitch(pitch-2)
        TNT1 A 0 A_FireCustomMissile("KharonGuard",0,0)
        //TNT1 A 0 A_FireCustomMissile("KharonAttackHeavyB",0,0)
        KATH D 1 A_TakeInventory("KharonSwung",1)
        KATH E 1
        TNT1 A 2
        TNT1 A 0 A_SetPitch(pitch+2)
        TNT1 A 2
        TNT1 A 0 A_SetPitch(pitch+1)
        TNT1 A 1
        TNT1 A 0 A_SetPitch(pitch+0.5)
        //TNT1 A 5 A_WeaponReady(WRF_NOPRIMARY | WRF_NOSWITCH)
        goto Fired2



    SpinAttackLeft:
        TNT1 A 1 A_SetAngle(angle+11.25)
        TNT1 A 1 A_SetAngle(angle+22.5)
        TNT1 A 0 A_TakeInventory("SuperMeterCounter",20)
        TNT1 A 0 A_PlayWeaponSound("sword/spinattack")
        TNT1 A 0 A_PlaySoundEx("haelin/attack","soundslot6",0,0)
        TNT1 A 0 A_PlaySoundEx("sword/swingheavy","soundslot7",0,0)
        TNT1 A 0 A_SpawnItemEx("KharonExplosion",0,0,0,0,0,0,0,48)
        TNT1 A 0 A_SpawnItemEx("ShockwaveSpawner",0,0,0,0,0,0,0,32)
        TNT1 A 0 A_CheckFloor("SpinAttackLeftContinue")
        TNT1 A 0 ThrustThingZ(0,24,0,0)
    SpinAttackLeftContinue:
        KSPC G 1 A_SetAngle(angle+45)
        KSPC H 1 A_SetAngle(angle+50)
        KSPC I 1 A_SetAngle(angle+55)
        KSPC J 1 A_SetAngle(angle+55)
        KSPC K 1 A_SetAngle(angle+50)
        KSPC L 1 A_SetAngle(angle+45)
        TNT1 A 1 A_SetAngle(angle+22.5)
        TNT1 A 1 A_SetAngle(angle+11.25)
        goto SpecialAttackFinish

    SpinAttackRight:
        TNT1 A 1 A_SetAngle(angle-11.25)
        TNT1 A 1 A_SetAngle(angle-22.5)
        TNT1 A 0 A_TakeInventory("SuperMeterCounter",20)
        TNT1 A 0 A_PlayWeaponSound("sword/spinattack")
        TNT1 A 0 A_PlaySoundEx("haelin/attack","soundslot6",0,0)
        TNT1 A 0 A_PlaySoundEx("sword/swingheavy","soundslot7",0,0)
        TNT1 A 0 A_SpawnItemEx("KharonExplosion",0,0,0,0,0,0,0,48)
        TNT1 A 0 A_SpawnItemEx("ShockwaveSpawner",0,0,0,0,0,0,0,32)
        TNT1 A 0 A_CheckFloor("SpinAttackRightContinue")
        TNT1 A 0 ThrustThingZ(0,28,0,0)
    SpinAttackRightContinue:
        KSPC M 1 A_SetAngle(angle-45)
        KSPC N 1 A_SetAngle(angle-50)
        KSPC O 1 A_SetAngle(angle-55)
        KSPC P 1 A_SetAngle(angle-55)
        KSPC Q 1 A_SetAngle(angle-50)
        KSPC R 1 A_SetAngle(angle-45)
        TNT1 A 1 A_SetAngle(angle-22.5)
        TNT1 A 1 A_SetAngle(angle-11.25)
        goto SpecialAttackFinish

    LungeAttack:
        TNT1 A 0 A_CheckFloor("LungeAttackRise")
    LungeAttackPrepare:
        TNT1 A 0 A_TakeInventory("SuperMeterCounter",20)
        TNT1 A 0 A_ChangeVelocity(0,0,0,CVF_REPLACE)
        TNT1 A 0 A_ChangeFlag("NOGRAVITY",1)
        TNT1 A 0 A_ChangeFlag("GHOST",1)
        TNT1 A 0 ACS_ExecuteAlways(267,0,WEEB_DEC_FREEZE,0,0)
        TNT1 A 0 A_ChangeFlag("THRUSPECIES",1)
        TNT1 A 1
        TNT1 A 0 A_PlayWeaponSound("sword/beam3")
        TNT1 A 0 A_PlaySoundEx("haelin/attack","soundslot6",0,0)
        TNT1 AA 0 A_SpawnItemEx("RazorWindExplosion",0,0,0,0,0,0,0,48)
        TNT1 A 0 A_SpawnItemEx("RazorWind",0,0,32,125,0,0,0,48)
        TNT1 A 0 A_SpawnItemEx("RazorWind",0,0,32,124,0,0,1,48)
        TNT1 A 0 A_SpawnItemEx("RazorWind",0,0,32,124,0,0,-1,48)
        TNT1 A 0 A_SpawnItemEx("RazorWind",0,0,32,123,0,0,2,48)
        TNT1 A 0 A_SpawnItemEx("RazorWind",0,0,32,123,0,0,-2,48)
        TNT1 A 0 A_SpawnItemEx("RazorWind",0,0,32,122,0,0,3,48)
        TNT1 A 0 A_SpawnItemEx("RazorWind",0,0,32,122,0,0,-3,48)
        TNT1 A 0 A_SpawnItemEx("RazorWind",0,0,32,121,0,0,4,48)
        TNT1 A 0 A_SpawnItemEx("RazorWind",0,0,32,121,0,0,-4,48)
        TNT1 A 0 ThrustThing(angle*256/360,120,1,0)
    LungeAttackContinue:
        KSPC ABCDEF 1
        TNT1 A 1
        TNT1 A 0 A_ChangeVelocity(0,0,0,CVF_REPLACE)
        TNT1 A 0 A_Stop // You'd think this would be redundant.
        TNT1 A 0 ACS_ExecuteAlways(267,0,WEEB_DEC_UNFREEZE,0,0)
        TNT1 A 5 A_SpawnItemEx("TelefragHack",0)
        TNT1 A 0 A_ChangeFlag("NOGRAVITY",0)
        TNT1 A 0 A_ChangeFlag("THRUSPECIES",0)
        TNT1 A 0 A_ChangeFlag("GHOST",0)
        goto SpecialAttackFinish
    LungeAttackRise:
        TNT1 A 1 ThrustThingZ(0,24,0,0)
        goto LungeAttackPrepare

    RepelWave:
        KBLK ABCDEE 1
        KBLK F 15 A_PlayWeaponSound("sword/block")
        KSPC STU 1
        TNT1 A 5
        KSPC VW 1
        TNT1 A 0 A_TakeInventory("SuperMeterCounter",20)
        TNT1 A 0 A_PlayWeaponSound("sword/reflect")
        TNT1 A 0 A_PlaySoundEx("haelin/attack","soundslot6",0,0)
        TNT1 A 0 A_FireCustomMissile("RazorWind2",0,0,0,0,0,0)
        TNT1 A 0 A_FireCustomMissile("RazorWind2",-3,0,0,0,0,0)
        TNT1 A 0 A_FireCustomMissile("RazorWind2",3,0,0,0,0,0)
        TNT1 A 0 A_FireCustomMissile("RazorWind2",-6,0,0,0,0,0)
        TNT1 A 0 A_FireCustomMissile("RazorWind2",6,0,0,0,0,0)
        TNT1 A 0 A_FireCustomMissile("RazorWind2",-9,0,0,0,0,0)
        TNT1 A 0 A_FireCustomMissile("RazorWind2",9,0,0,0,0,0)
        TNT1 A 0 A_FireCustomMissile("RazorWind2",-12,0,0,0,0,0)
        TNT1 A 0 A_FireCustomMissile("RazorWind2",12,0,0,0,0,0)
        TNT1 A 0 A_FireCustomMissile("RazorWind2",-15,0,0,0,0,0)
        TNT1 A 0 A_FireCustomMissile("RazorWind2",15,0,0,0,0,0)
        KSPC XYZ 1
        TNT1 AA 1
        goto SpecialAttackFinish

    SpecialAttackFinish:
        TNT1 A 0 A_TakeInventory("SwordAttack",0x7FFFFFFF)
        TNT1 A 0 A_TakeInventory("KharonSwung",1)
        TNT1 A 0 A_TakeInventory("SlashingLikeAGaijin",1)
        TNT1 A 0 A_TakeInventory("ShootingLikeABaka",1)
        TNT1 A 0 A_TakeInventory("DoubleTapBack", 1)
        TNT1 A 0 A_TakeInventory("DoubleTapLeft", 1)
        TNT1 A 0 A_TakeInventory("DoubleTapRight", 1)
        TNT1 A 0 A_TakeInventory("DoubleTapForward", 1)
        goto Fired2



    AltFire:
        TNT1 A 0 A_JumpIfInventory("SlashingLikeAGaijin",1,"AltFirePrepare")
        TNT1 A 0 A_JumpIfInventory("ShootingLikeABaka",1,"AltFireGun")
        WEPE EFG 1
        TNT1 A 0 A_GiveInventory("ShootingLikeABaka",1)
    AltFireGun:
        GLAG A 1 A_WeaponReady(WRF_NOFIRE | WRF_NOSWITCH)
        TNT1 A 0 A_JumpIfNoAmmo("Nope")
        GLAG B 1 A_WeaponReady(WRF_NOFIRE | WRF_NOSWITCH)
        TNT1 A 0 A_Light1
        TNT1 A 0 A_AlertMonsters
        TNT1 A 0 A_PlayWeaponSound("carronade/fire")
        TNT1 A 0 A_FireCustomMissile("ExodusRocket",random(-350,350)/100.00,1,5,1,0,random(10,250)/100.00)
        TNT1 A 0 A_Recoil(2)
        TNT1 A 0 Radius_Quake(2,3,0,16,0)
        TNT1 A 0 A_JumpIfInventory("IAmASillyPersonWhoDoesntLikeRecoil",1,"NoRecoil")
        TNT1 A 0 A_SetAngle(angle+(random(-2000,2000)/1000.000))
        TNT1 A 0 A_SetPitch(pitch-(random(800,1800)/1000.000))
    NoRecoil:
        GLAG CD 1 Bright A_WeaponReady(WRF_NOFIRE | WRF_NOSWITCH)
        GLAG EEEE 1 A_WeaponReady(WRF_NOFIRE | WRF_NOSWITCH)
        TNT1 A 0 A_PlaySoundEx("carronade/turn","soundslot5",0,0)
        TNT1 A 0 A_Light0
        GLAG FGHIJKLM 1 A_WeaponReady(WRF_NOFIRE | WRF_NOSWITCH)
        GLAG AA 1 A_WeaponReady(WRF_NOSWITCH)
        GLAG A 70 A_WeaponReady
        Goto AltFired
    Nope:
        TNT1 A 0 A_PlaySoundEx("carronade/dry","soundslot7",0,0)
        GLAG A 10 A_WeaponReady(WRF_NOSECONDARY | WRF_NOSWITCH)
        TNT1 A 0 A_JumpIfInventory("IAmAnOkayPersonWhoLikesToAutoSwitch",1,"DeselectShit")
        GLAG A 35 A_Weaponready
        Goto AltFired
    AltFirePrepare:
        WEPE H 1 A_TakeInventory("KharonSwung",1)
        TNT1 A 0 A_TakeInventory("SlashingLikeAGaijin",1)
        TNT1 A 0 A_GiveInventory("ShootingLikeABaka",1)
        WEPE I 1
        goto AltFireGun
    AltFired:
        TNT1 A 0 A_TakeInventory("KharonSwung",1)
        TNT1 A 0 A_TakeInventory("SlashingLikeAGaijin",1)
        TNT1 A 0 A_TakeInventory("ShootingLikeABaka",1)
        TNT1 A 0 A_ClearRefire
        WEPE GFE 1
        Goto Ready

    HenshinCheck:
        TNT1 A 0 A_JumpIfInventory("SuperMeterCounter",100,"Henshin")
        TNT1 A 1
        TNT1 A 0 A_PlaySoundEx("henshin/error2","soundslot5",0,0)
        TNT1 A 0 A_PlaySoundEx("henshin/error","soundslot6",0,0)
        TNT1 A 0 A_PlaySoundEx("*pain","sounslot7",0,0)
        TNT1 A 0 A_SetBlend("White",1.0,35)
        TNT1 A 0 A_Recoil(10)
        TNT1 A 0 A_TakeInventory("HenshinActivated",1)
        TNT1 A 0 A_ClearRefire
        goto Fired2
    Henshin:
        WEPE BCD 1
        TNT1 A 1
        TNT1 A 0 A_PlaySoundEx("henshin/click","soundslot6",0,0)
        TNT1 A 4 A_TakeInventory("KharonSwung",1)
        TNT1 A 0 A_PlayWeaponSound("henshin/foley")
        HNSH AABBCDE 1
        HNSH F 15 A_PlaySoundEx("henshin/call","soundslot6",0,0)
        TNT1 A 0 A_PlayWeaponSound("henshin/foley")
        HNSH FGHIIJJKKLL 1
        TNT1 A 0 A_PlaySoundEx("hammer/spark","soundslot5",0,0)
        TNT1 A 0 A_PlayWeaponSound("henshin/foley")
        HNSH MNOOPP 1
        TNT1 A 0 A_PlayWeaponSound("henshin/foley")
        HNSH QRST 1
        TNT1 A 0 A_PlaySoundEx("henshin/donarmor","soundslot6",0,0)
        HNSH UVWX 2
        HNSH Y 2 A_SetBlend("Red",0.1,3)
        HNSH Z 2 A_SetBlend("Red",0.2,3)
        HNSN A 2 A_SetBlend("Red",0.3,3)
        HNSN B 2 A_SetBlend("Red",0.5,3)
        HNSN C 2 A_SetBlend("Red",0.7,3)
        HNSN D 2 A_SetBlend("Red",0.9,3)
        HNSN D 2 A_SetBlend("Red",1.0,70)
        TNT1 A 0 A_GiveInventory("Iron Fist",1)
        TNT1 A 0 A_SelectWeapon("Iron Fist")
        TNT1 A 0 A_SpawnItemEx("IronMaidenRepel",0,0,0,0,0,0,0,48)
        TNT1 A 0 ACS_ExecuteAlways(275,0,WEEB_DEC_CHANGEMUS,0,0)
        TNT1 A 0 A_TakeInventory("AlreadyInLevel",1)
        TNT1 A 0 A_TakeInventory("Kharon + Legion",1)
        TNT1 A 0 A_TakeInventory("Kharon + Frosthammer",1)
        TNT1 A 0 A_TakeInventory("Kharon + Testament",1)
        TNT1 A 0 A_TakeInventory("Kharon + Sabbath",1)
        TNT1 A 0 A_TakeInventory("Kharon + Exodus",1)
        TNT1 A 0 A_TakeInventory("Kharon + Omen",1)
        TNT1 A 0 A_TakeInventory("Kharon + Acacia A-22",1)
        TNT1 A 0 A_TakeInventory("Kharon",1)
        TNT1 A 0 A_TakeInventory("HenshinActivated",1)
        TNT1 A 0 A_TakeInventory("SuperMeterCounter",40)
        TNT1 A 0 A_GiveInventory("IronMaidenArmor2",100)
        TNT1 A 0 A_GiveInventory("InIronMaiden",1)
        TNT1 A 0 A_GiveInventory("IronMaidenSpeed",1)
        TNT1 A 1
        goto Deselect



    DeselectShit:
        TNT1 A 0 A_JumpIfInventory("GotCarronade",1,"ConsiderExodus")
    Consider3:
        TNT1 A 0 A_JumpIfInventory("GotUzi",1,"ConsiderSabbath")
    Consider2:
        TNT1 A 0 A_JumpIfInventory("GotShotgun",1,"ConsiderTestament")
    Consider1:
        TNT1 A 0 A_JumpIfInventory("GotHammer",1,"ConsiderOmen")
    Consider0:
        TNT1 A 0 A_JumpIfInventory("WeebClip",1,"SelectAcacia")
        TNT1 A 0 A_SelectWeapon("Kharon")
        TNT1 AAAAAAAAAAAAAAAAAAAA 0 A_Lower
        TNT1 A 1 A_Lower
        goto Deselect
        
    ConsiderExodus:
        TNT1 A 0 A_JumpIfInventory("WeebRocket",1,"SelectExodus")
        goto Consider3
    ConsiderSabbath:
        TNT1 A 0 A_JumpIfInventory("WeebCell",1,"SelectSabbath")
        goto Consider2
    ConsiderTestament:
        TNT1 A 0 A_JumpIfInventory("WeebShell",1,"SelectTestament")
        goto Consider1
    ConsiderOmen:
        TNT1 A 0 A_JumpIfInventory("HammerCharge",50,"SelectOmen")
        goto Consider0

    SelectExodus:
        TNT1 A 0 A_SelectWeapon("Kharon + Exodus")
        TNT1 AAAAAAAAAAAAAAAAAAAA 0 A_Lower
        TNT1 A 1 A_Lower
        goto Deselect
    SelectSabbath:
        TNT1 A 0 A_SelectWeapon("Kharon + Sabbath")
        TNT1 AAAAAAAAAAAAAAAAAAAA 0 A_Lower
        TNT1 A 1 A_Lower
        goto Deselect
    SelectTestament:
        TNT1 A 0 A_SelectWeapon("Kharon + Testament")
        TNT1 AAAAAAAAAAAAAAAAAAAA 0 A_Lower
        TNT1 A 1 A_Lower
        goto Deselect
    SelectOmen:
        TNT1 A 0 A_SelectWeapon("Kharon + Omen")
        TNT1 AAAAAAAAAAAAAAAAAAAA 0 A_Lower
        TNT1 A 1 A_Lower
        goto Deselect
    SelectAcacia:
        TNT1 A 0 A_SelectWeapon("Kharon + Acacia A-22")
        TNT1 AAAAAAAAAAAAAAAAAAAA 0 A_Lower
        TNT1 A 1 A_Lower
        goto Deselect
    }
}

ACTOR ExodusRocket
{
    Radius 6
    Height 4
    Damage (10)
    Speed 50
    Species "Player"
    Projectile
    Decal Scorch
    +THRUSPECIES
    +THRUGHOST
    +RANDOMIZE
    +GRENADETRAIL
    -NOGRAVITY
    +SKYEXPLODE
    Gravity 0.25
    Scale 0.4
    DeathSound "grenades/explode"
    States
    {
    Spawn:
        HORN A 1 Bright A_CustomMissile("ExodusRocketFlameSpawner",2,0,0,0)
        TNT1 A 0 A_CustomMissile("ExodusRocketFlameSpawner2",2,0,random(-100,100)/150.00,2,random(-100,100)/150.00)
        Loop
    Death:
        TNT1 A 0 A_Scream
		TNT1 A 0 A_SpawnItemEx("ExodusRocketDeathSFX",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
        TNT1 A 0 A_SpawnItemEx("ExodusRocketDamage",0,0,0,0,0,0,0,48)
        TNT1 A 0 A_SpawnItemEx("ExodusRocketDamage2",0,0,0,0,0,0,0,48)
        TNT1 A 0 A_SpawnItemEx("ExodusRocketJumpage",0,0,0,0,0,0,0,48)
        TNT1 A 0 Radius_Quake(5,3,0,8,0)
        TNT1 A 0 A_ChangeFlag("NOGRAVITY",1)
        TNT1 A 1
        Stop
    }
}

ACTOR ExodusRocketDeathSFX
{
    +NOCLIP
    +NOGRAVITY
    +THRUACTORS
    +NOINTERACTION
    +CLIENTSIDEONLY
    Renderstyle None
    States
    {
    Spawn:
        TNT1 A 0
        TNT1 A 0
        TNT1 A 0 ACS_ExecuteWithResult(275,WEEB_DEC_TOASTER)
        //TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(275,WEEB_DEC_TOASTER2) <= 0,"Toaster0")
        //TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(275,WEEB_DEC_TOASTER2) == 1,"Toaster1")
        //TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(275,WEEB_DEC_TOASTER2) >= 2,"Toaster2")
        TNT1 A 1
        Loop
    Toaster0:
        TNT1 A 0
        TNT1 A 0
        TNT1 A 0
        TNT1 A 0
        TNT1 A 0
        TNT1 AAAAAAAAAAAAAAA 0 A_SpawnItemEx("Firetrail",0,0,0,random(-100,100)/10.0,random(-100,100)/10.0,random(-100,100)/10.0)
        TNT1 AAAAAAAAAAAAAAA 0 A_SpawnItemEx("Firetrail",0,0,0,random(-100,100)/10.0,random(-100,100)/10.0,random(-100,100)/10.0)
        TNT1 AAAAAAAAAAAAAAA 0 A_SpawnItemEx("Firetrail",0,0,0,random(-100,100)/10.0,random(-100,100)/10.0,random(-100,100)/10.0)
        TNT1 AAAAAAAAAAAAAAA 0 A_SpawnItemEx("Firetrail",0,0,0,random(-100,100)/10.0,random(-100,100)/10.0,random(-100,100)/10.0)
        TNT1 AAAA 0 A_SpawnItemEx("MissileExplosionSmall",0,0,0,0,0,0,0,160)
        TNT1 AAA 0 A_SpawnItemEx("MissileExplosionLarge",0,0,0,0,0,0,0,160)
        TNT1 AAA 0 A_SpawnItemEx("MissileExplosionSmall",random(-50,50)/100.00,random(-50,50)/100.00,random(-50,50)/100.00,random(-250,250)/100.00,random(-250,250)/100.00,random(-250,250)/100.00,random(-180,180),160)
        TNT1 AA 0 A_SpawnItemEx("MissileExplosionLarge",random(-50,50)/100.00,random(-50,50)/100.00,random(-50,50)/100.00,random(-250,250)/100.00,random(-250,250)/100.00,random(-250,250)/100.00,random(-180,180),160)
        TNT1 A 1
        Stop
    Toaster1:
        TNT1 A 0
        TNT1 A 0
        TNT1 A 0
        TNT1 A 0
        TNT1 A 0
        TNT1 AAAAAAAAAAAAAAA 0 A_SpawnItemEx("Firetrail",0,0,0,random(-100,100)/10.0,random(-100,100)/10.0,random(-100,100)/10.0)
        TNT1 AAAAA 0 A_SpawnItemEx("Firetrail",0,0,0,random(-100,100)/10.0,random(-100,100)/10.0,random(-100,100)/10.0)
        TNT1 AA 0 A_SpawnItemEx("MissileExplosionSmall",0,0,0,0,0,0,0,160)
        TNT1 A 0 A_SpawnItemEx("MissileExplosionLarge",0,0,0,0,0,0,0,160)
        TNT1 AA 0 A_SpawnItemEx("MissileExplosionSmall",random(-50,50)/100.00,random(-50,50)/100.00,random(-50,50)/100.00,random(-250,250)/100.00,random(-250,250)/100.00,random(-250,250)/100.00,random(-180,180),160)
        TNT1 A 0 A_SpawnItemEx("MissileExplosionLarge",random(-50,50)/100.00,random(-50,50)/100.00,random(-50,50)/100.00,random(-250,250)/100.00,random(-250,250)/100.00,random(-250,250)/100.00,random(-180,180),160)
        TNT1 A 1
        Stop
    Toaster2:
        TNT1 A 0
        TNT1 A 0
        TNT1 A 0
        TNT1 A 0
        TNT1 A 0
        TNT1 A 0 A_SpawnItemEx("MissileExplosionSmall",0,0,0,0,0,0,0,160)
        TNT1 A 0 A_SpawnItemEx("MissileExplosionLarge",0,0,0,0,0,0,0,160)
        TNT1 A 1
        Stop
   }
}

ACTOR ExodusRocketDamage
{
    Radius 1
    Height 1
    Speed 0
    +NOCLIP
    -SOLID
    +DONTBLAST
    DamageType "ExodusDamage"
    Obituary "%k consumed %o in fire."
    States
    {
      Spawn:
        TNT1 A 0
        TNT1 A 1 A_Explode(80,256,0)
        Stop
    }
}

actor ExodusRocketDamage2 : ExodusRocketDamage { +FORCERADIUSDMG }

ACTOR ExodusRocketJumpage
{
    Radius 1
    Height 1
    Speed 0
    +NOCLIP
    +FORCERADIUSDMG
    +BLOODLESSIMPACT
    -SOLID
    +DONTBLAST
    DamageType "ExodusJump"
    Obituary "%k shouldn't have killed %o with this."
    States
    {
      Spawn:
        TNT1 A 0
        TNT1 A 0 A_GiveInventory("GrossHack",1)
        TNT1 A 1 A_Explode(160,192,1)
        Stop
    }
}

actor GrossHack : ArtiBlastRadius { +INVENTORY.AUTOACTIVATE }

ACTOR ExodusRocketFlameSpawner
{
    Projectile
    Speed 0
    Scale 0.15
    Alpha 0.5
    Height 0
    Renderstyle Add
    +NOCLIP
    +NOTELEPORT
    +NOBLOCKMAP
    -FLOORCLIP
    +DONTBLAST
    +CLIENTSIDEONLY
    States
    {
    Spawn:
        TNT1 A 0
        TNT1 A 0
        TNT1 A 0 ACS_ExecuteWithResult(275,WEEB_DEC_TOASTER)
        //TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(275,WEEB_DEC_TOASTER2) <= 0,"Toaster0")
        //TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(275,WEEB_DEC_TOASTER2) == 1,"Toaster1")
        //TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(275,WEEB_DEC_TOASTER2) >= 2,"Toaster2")
        TNT1 A 1 BRIGHT 
        Loop
    Toaster0:
        TNT1 AA 1
        XPLO AB 2 BRIGHT 
        XPLO C 2 BRIGHT A_CustomMissile("ExodusRocketFlameTrail",1,random(-100,100)/100.00,2,random(-100,100)/100.00)
        Stop
    Toaster1:
        TNT1 AA 1
        XPLO AB 2 BRIGHT 
		TNT1 A 0 A_Jump(128,"Toaster1A")
        XPLO C 2 BRIGHT A_CustomMissile("ExodusRocketFlameTrail",1,random(-100,100)/100.00,2,random(-100,100)/100.00)
        Stop
    Toaster1A:
        XPLO C 2 BRIGHT 
        Stop
    Toaster2:
        //TNT1 A 0
        TNT1 AA 1
        XPLO AB 2 BRIGHT 
        XPLO C 2 BRIGHT
		Stop
    }
}

ACTOR ExodusRocketFlameSpawner2 : ExodusRocketFlameSpawner 
{ 
	Scale 0.1 
    States
    {
    Toaster2:
        TNT1 A 0
		Stop
    }
}

ACTOR ExodusRocketFlameTrail
{
    PROJECTILE
    +DONTBLAST
    Height 1
    Radius 1
    Scale 0.16
    Alpha 0.3
    Renderstyle Translucent
    States
    {
    Spawn:
        SMOK ABCDEFGHIJKLMNOPQ 1
        Stop
    }
}

actor MissileExplosionSmall
{
    RenderStyle Add
    Alpha 0.9
    Scale 0.75
    +CLIENTSIDEONLY
    +NOINTERACTION
    +DONTBLAST
    States
    {
    Spawn:
        TNT1 A 0
        TNT1 A 0 A_Jump(256,"Spawn1","Spawn2","Spawn3","Spawn4","Spawn5","Spawn6","Spawn7")
        TNT1 A 1
        stop
    Spawn1:
        MXP1 ABCDEFGHIJKLMNOPQRSTUVWXYZ 2 BRIGHT
        stop
    Spawn2:
        MXP2 ABCDEFGHIJKLMNOPQRSTUVWXYZ 2 BRIGHT
        stop
    Spawn3:
        MXP3 ABCDEFGHIJKLMNOPQRSTUVWXYZ 2 BRIGHT
        stop
    Spawn4:
        MXP4 ABCDEFGHIJKLMNOPQRSTUVWXYZ 2 BRIGHT
        stop
    Spawn5:
        MXP5 ABCDEFGHIJKLMNOPQRSTUVWXYZ 2 BRIGHT
        stop
    Spawn6:
        MXP6 ABCDEFGHIJKLMNOPQRSTUVWXYZ 2 BRIGHT
        stop
    Spawn7:
        MXP7 ABCDEFGHIJKLMNOPQRSTUVWXYZ 2 BRIGHT
        stop
    }
}

actor MissileExplosionLarge : MissileExplosionSmall
{
    Alpha 0.65
    Scale 1.25
}

Actor FireTrail
{
    Radius 4
    Height 2
    Scale 0.4
    Gravity 0.2
    BounceCount 2
    RenderStyle Add
    Alpha 0.99
    Projectile
    -NOGRAVITY
    +EXPLODEONWATER
    +NOTELEPORT
    +DOOMBOUNCE
    +CLIENTSIDEONLY
    +DONTBLAST
    States
    {
    Spawn:
        1090 Q 1 BRIGHT A_FadeOut(0.2)
        1090 R 1 BRIGHT A_SpawnItemEx("FireTrail2",0,0,0,random(0,200)/100.00,random(-200,0)/100.00,0)
        1090 S 1 BRIGHT
        1090 T 1 BRIGHT A_SpawnItemEx("FireTrail2",0,0,0,random(0,200)/100.00,random(-200,0)/100.00,0)
        1090 U 1 BRIGHT
        1090 V 1 BRIGHT A_SpawnItemEx("FireTrail2",0,0,0,random(0,200)/100.00,random(-200,0)/100.00,0)
        1090 W 1 BRIGHT
        1090 X 1 BRIGHT A_SpawnItemEx("FireTrail2",0,0,0,random(0,200)/100.00,random(-200,0)/100.00,0)
        loop
    death:
        1090 QRSTUVWX 1
        stop
    }
}

Actor FireTrail2 : FireTrail
{
    States
    {
    Spawn:
        1091 ABCDE 3 BRIGHT
        stop
    }
}